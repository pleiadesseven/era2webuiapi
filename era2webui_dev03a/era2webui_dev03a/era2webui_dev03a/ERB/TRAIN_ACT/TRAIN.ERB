;=============================================================================
;ソース・パラメータ変動処理他
;=============================================================================
@SOURCE_CHECK
;-------------------------------------------------
;今回実行したコマンドの種類をTFLAG:9に記録
;-------------------------------------------------
;失敗、ファンブル
SIF !EQUIP:戦闘中 || !TFLAG:552
	CALL CHECK_COM_KIND

;-------------------------------------------------
;コマンド実行時の口上
;-------------------------------------------------
;失敗、ファンブル
IF !EQUIP:戦闘中 || !TFLAG:552
	;調教者側の口上表示
	CALL DISPLAY_KOJO_TRAIN_MESSAGE_TRAINER_COM
	;調教対象の口上表示
	SIF TFLAG:899 == 0
		CALL DISPLAY_KOJO_TRAIN_MESSAGE_S_COM
ENDIF

;-------------------------------------------------
;同性のチェック、調教者の能力のチェック
;-------------------------------------------------
IF EQUIP:睡眠薬 == 0
	CALL SOURCE_SEX_CHECK
	IF ASSIPLAY == 0 && (TALENT:PLAYER:オトコ ^^ TALENT:オトコ) && TALENT:恋慕
		IF FLAG:3 == 1
			CALL SOURCE_SEX_MALE_CHECK_EASY
		ELSE
			CALL SOURCE_SEX_MALE_CHECK_NORMAL
		ENDIF
	ENDIF
	CALL PLAYER_SKILL_CHECK
	CALL MASTER_SKILL_CHECK
	SIF CFLAG:41
		CALL SOURCE_INPLANT
	;EASYでなくて、失神していなければストレスによる影響がある
	SIF FLAG:3 > 1 && TFLAG:899 > 0
		CALL SOURCE_STRESS_CHECK
ENDIF

;-------------------------------------------------
;装着道具関連のチェック
;-------------------------------------------------
CALL CHECK_TEQUIPMENT_TRAIN

;-------------------------------------------------
;道具装着中に毎ターン表示するメッセージ
;-------------------------------------------------
;浣腸プラグ
SIF TEQUIP:Ａ系装着器具 == 7
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 4, 0

;-------------------------------------------------
;処女喪失のチェック
;-------------------------------------------------
IF TCVAR:Ｖ経験 && TALENT:処女 && TFLAG:1
	SETCOLOR 0xF06050
	PRINTW 処女喪失
	RESETCOLOR
	;処女食い回数カウント
	IF TFLAG:2 && TALENT:処女 == 1
		FLAG:400 += 1
		IF FLAG:400 >= 50
			;実績
			LOADGLOBAL
			SIF !(GLOBAL:2300 & 8)
				GLOBAL:2300 |= 8
			SAVEGLOBAL
		ENDIF
	ENDIF
	TALENT:処女 = 0
	STAIN:ヴァギナ |= 32

	;処女喪失フラグ(今回のコマンド)
	TFLAG:0 = 1
	;処女喪失フラグ(今回の調教)
	TFLAG:101 = 1
	;処女喪失フラグ(ビデオ撮影)
	SIF TEQUIP:ビデオ撮影
		TFLAG:120 |= 1
	SIF FLAG:12 & 8
		CALL SWEET_POTATO_VIRGIN_BLOOD
ENDIF

;-------------------------------------------------
;気力切れによる時間停止の強制解除
;-------------------------------------------------
SIF TEQUIP:時間止め == 1 && BASE:(TFLAG:158):1 <= 0
	CALL TIMESTOP_RELEASE

;-------------------------------------------------
;ソース上限値チェック(eratohoJを参考にしました)
;-------------------------------------------------
;ソースを100万でカンストさせる
REPEAT 42
	SOURCE:COUNT = LIMIT(SOURCE:COUNT, 0, 1000000)
REND

;-------------------------------------------------
;死亡モード１
;-------------------------------------------------
IF TALENT:死亡
	REPEAT 43
		LOCAL = COUNT
		SIF STRLENS(SOURCENAME:LOCAL) <= 0 || LOCAL == 19
			CONTINUE
		SOURCE:LOCAL = 0
	REND
ENDIF

;-------------------------------------------------
;快Ｃのソース
;-------------------------------------------------
CALL CALC_SOURCE0

;-------------------------------------------------
;快Ｖのソース
;-------------------------------------------------
CALL CALC_SOURCE1

;-------------------------------------------------
;快Ａのソース
;-------------------------------------------------
CALL CALC_SOURCE2

;-------------------------------------------------
;快Ｂのソース
;-------------------------------------------------
CALL CALC_SOURCE3

;-------------------------------------------------
;同じコマンドの連続実行による上下の処理(快感系)
;-------------------------------------------------
;UP:0～3は絶頂に絡むので先に処理してある
;同一コマンド連続実行ペナルティ回避フラグがオンの時は飛ばす
SIF SELECTCOM == PREVCOM && TFLAG:96 == 0
	CALL SEQUENCE_COM_ECSTASY

;-------------------------------------------------
;残り気力や欲情・抑鬱による上下の処理(快感系)
;-------------------------------------------------
CALL UP_VIGOR_CVA_CHECK

;-------------------------------------------------
;相性による上下の処理(快感系)
;-------------------------------------------------
LOCAL = NO:PLAYER
IF RELATION:LOCAL != 0
	RELATION:LOCAL = LIMIT (RELATION:LOCAL, 0, 1000)
	REPEAT 4
		UP:COUNT *= RELATION:LOCAL
		UP:COUNT /= 100
	REND
ENDIF

;-------------------------------------------------
;素質などによる上下の処理(快感系)
;-------------------------------------------------
CALL UP_TALENT_CVA_CHECK

;-------------------------------------------------
;理性処理(快感系)
;-------------------------------------------------
LOCAL:0 = (UP:0 + UP:1 + UP:2 + UP:3) / 15
LOCAL:0 = TABLE_CALC(LOCAL:0, 250, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 500, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 750, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 1000, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 1300, 3)
LOCAL:0 = TABLE_CALC(LOCAL:0, 1600, 3)
LOCAL:0 = TABLE_CALC(LOCAL:0, 1900, 3)
LOCAL:0 = TABLE_CALC(LOCAL:0, 2200, 3)
LOCAL:0 = TABLE_CALC(LOCAL:0, 2500, 4)
LOCAL:0 = TABLE_CALC(LOCAL:0, 2800, 4)
LOCAL:0 = TABLE_CALC(LOCAL:0, 3200, 4)
LOCAL:0 = TABLE_CALC(LOCAL:0, 3600, 4)
LOCAL:0 = TABLE_CALC(LOCAL:0, 4000, 5)
LOCAL:0 = TABLE_CALC(LOCAL:0, 4400, 5)
LOCAL:0 = TABLE_CALC(LOCAL:0, 4800, 5)
LOCAL:0 = TABLE_CALC(LOCAL:0, 5200, 5)
LOCAL:0 = TABLE_CALC(LOCAL:0, 5600, 6)
LOCAL:0 = TABLE_CALC(LOCAL:0, 6000, 6)
DOWNBASE:9 += LOCAL:0

;-------------------------------------------------
;愛液処理
;-------------------------------------------------
IF !TALENT:死亡
	LOCAL:1 = UP:0 + UP:1 + UP:2 + UP:3
	TIMES LOCAL:1 , 0.85
	;機械持ちの場合には陥落していなくて快感に素直もないと分泌しずらい(盛り上がりにくい)
	SIF TALENT:機械 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0 && TALENT:快感に素直 == 0
		TIMES LOCAL:1 , 0.2
	;快Ｃ＋快Ｖ＋快Ａ＋快Ｂが合わせて100以上上昇ならその20%の液体のソースが加わる（オトコはダメ）
	IF LOCAL:1 > 100 && TALENT:オトコ == 0
		;濡れやすいと濡れにくいはここで処理
		IF TALENT:濡れやすい
			TIMES LOCAL:1 , 2.50
		ELSEIF TALENT:濡れにくい
			TIMES LOCAL:1 , 0.40
		ENDIF
		SOURCE:液体追加 += LOCAL:1 / 5
	ENDIF
ENDIF

;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
;賢者の血が効果を発揮しているときは別処理
IF TEQUIP:賢者の血 > 1
	;賢者の血の効果処理
	CALL ECST_CANCEL
ELSE
	CALL ORGASM_PROCESS
ENDIF

;-------------------------------------------------
;まだ射精していない場合に調教対象の射精チェック(ふたなり・オトコ)
;-------------------------------------------------
SIF TFLAG:705 == 0 && (TALENT:オトコ || TALENT:ふたなり)
	CALL TARGET_EJAC_CHECK
TFLAG:705 = 0

;-------------------------------------------------
;調教対象の噴乳チェック(母乳体質)
;-------------------------------------------------
SIF TALENT:母乳体質
	CALL TARGET_MILK_CHECK

;-------------------------------------------------
;調教対象の放尿チェック(お漏らし癖・利尿剤・カテーテル・放尿経験)
;-------------------------------------------------
; || (EXP:放尿経験 + TCVAR:放尿経験)
;SIF TEQUIP:利尿剤 || TEQUIP:Ｕ系装着器具 || TALENT:おもらし癖
	CALL TARGET_PEE_CHECK

;-------------------------------------------------
;調教対象の調教度チェック
;-------------------------------------------------
CALL TARGET_ACCEPTANCE_CHECK

;-------------------------------------------------
;調教対象の精飲絶頂チェック(精愛味覚or絶頂＋精液を飲ませた)
;-------------------------------------------------
;口内射精の時も精液を飲ませた扱い
SIF TFLAG:15
	TFLAG:3 += TFLAG:15
SIF TFLAG:3 && (TALENT:精愛味覚 || TFLAG:51) && FLAG:14 & 256
	CALL TARGET_SEIIN_CHECK

;-------------------------------------------------
;主人(あなた)による調教経験＆調教度
;-------------------------------------------------
SIF ASSIPLAY == 0
	CALL MASTER_FLAG_CHECK
CALL TARGET_ACCEPTANCE_MAIN

LOCAL = 0
TFLAG:50 = 0

;-------------------------------------------------
;助手による調教経験（妊娠パッチ用）
;-------------------------------------------------
SIF ASSIPLAY && TEQUIP:触手調教 == 0
	CALL ASSI_EXP_COUNT

;-------------------------------------------------
;接触のソース
;-------------------------------------------------
CALL CALC_SOURCE10

;-------------------------------------------------
;情愛のソース
;-------------------------------------------------
CALL CALC_SOURCE11

;-------------------------------------------------
;液体追加のソース
;-------------------------------------------------
;結果が露出に影響するのでこっちに持ってきた
CALL CALC_SOURCE19

;-------------------------------------------------
;露出のソース
;-------------------------------------------------
CALL CALC_SOURCE12

;-------------------------------------------------
;主導権のソース
;-------------------------------------------------
CALL CALC_SOURCE14

;-------------------------------------------------
;性行動のソース
;-------------------------------------------------
CALL CALC_SOURCE15

;-------------------------------------------------
;達成のソース
;-------------------------------------------------
CALL CALC_SOURCE16

;-------------------------------------------------
;恭順追加のソース
;-------------------------------------------------
CALL CALC_SOURCE17

;-------------------------------------------------
;欲情追加のソース
;-------------------------------------------------
CALL CALC_SOURCE18

;-------------------------------------------------
;痛覚のソース
;-------------------------------------------------
CALL CALC_SOURCE20

;-------------------------------------------------
;痒みのソース
;-------------------------------------------------
CALL CALC_SOURCE21

;-------------------------------------------------
;目覚め判定
;--------------------------------------------------
;こんな中途半端な位置にあるのは
;屈従/逸脱のソース発生の可能性があるから
SIF EQUIP:睡眠薬
	CALL WAKE_CHECK

;-------------------------------------------------
;屈従のソース
;-------------------------------------------------
CALL CALC_SOURCE22

;-------------------------------------------------
;支配のソース
;-------------------------------------------------
CALL CALC_SOURCE23

;-------------------------------------------------
;中毒充足のソース
;-------------------------------------------------
CALL CALC_SOURCE24

;-------------------------------------------------
;トラウマのソース
;-------------------------------------------------
CALL CALC_SOURCE25

;-------------------------------------------------
;薬物侵蝕のソース
;-------------------------------------------------
CALL CALC_SOURCE26

;-------------------------------------------------
;触手汚染のソース
;-------------------------------------------------
CALL CALC_SOURCE27

;-------------------------------------------------
;羞恥扇情のソース
;-------------------------------------------------
CALL CALC_SOURCE28

;-------------------------------------------------
;性技学習のソース
;-------------------------------------------------
CALL CALC_SOURCE29

;-------------------------------------------------
;不潔のソース
;-------------------------------------------------
CALL CALC_SOURCE30

;-------------------------------------------------
;逸脱のソース
;-------------------------------------------------
CALL CALC_SOURCE31

;-------------------------------------------------
;反感追加のソース
;-------------------------------------------------
CALL CALC_SOURCE32

;-------------------------------------------------
;酒酔いのソース
;-------------------------------------------------
CALL CALC_SOURCE40

;-------------------------------------------------
;酒酔い(鬼殺し)のソース
;-------------------------------------------------
CALL CALC_SOURCE41

;-------------------------------------------------
;媚薬浸潤のソース
;-------------------------------------------------
SIF SOURCE:媚薬浸潤
	CALL CALC_SOURCE42, TARGET
SIF ASSI >= 0 && SOURCE:ASSI:媚薬浸潤
	CALL CALC_SOURCE42, ASSI

;-------------------------------------------------
;触手を寄生させているときの処理
;-------------------------------------------------
IF FLAG:15 & 4096
	SIF TALENT:寄生
		CALL PARASITIC_TENTACLE_TARGET
	SIF ASSI >= 0 && TALENT:ASSI:寄生
		CALL PARASITIC_TENTACLE_ASSI
ENDIF

;-------------------------------------------------
;酒酔いチェック
;-------------------------------------------------
SIF BASE:酔い > 0
	CALL ALCOHOL_DRUNK_CHECK,TARGET
;もし主人が酔っていたら
SIF BASE:MASTER:酔い > 0
	CALL ALCOHOL_DRUNK_CHECK,MASTER
;もし助手が酔っていたら
SIF ASSI >= 0 && BASE:ASSI:酔い > 0
	CALL ALCOHOL_DRUNK_CHECK,ASSI

;-------------------------------------------------
;調教対象の失神中の処理及び失神回復チェック
;失神回復時にUPに加算する処理があり、素質などによる補正をかけたいのでUP_TALENT_CHECKよりも前に置く
;-------------------------------------------------
;コンフィグで失神モードがオンであり、失神したターンの次のターン以降
SIF FLAG:16 & 2 && TFLAG:899 >= 1 && !TALENT:死亡
	CALL PASSOUT_DURING

;-------------------------------------------------
;素質などによる上下の処理(快感系以外)
;-------------------------------------------------
CALL UP_TALENT_CHECK

;-------------------------------------------------
;相性による上下の処理
;-------------------------------------------------
;オーバーフローが怖いので相性最大は1000
;それ以上もしくは負の値の相性だとここで0～1000に収まるように修正
RELATION:(NO:PLAYER) = LIMIT (RELATION:(NO:PLAYER), 0, 1000)
LOCAL = TRAIN_RELATION_CALC(PLAYER)
IF LOCAL > 0 && LOCAL != 100
	UP:11 *= LOCAL
	UP:11 /= 100
	UP:12 *= LOCAL
	UP:12 /= 100
	UP:13 *= LOCAL
	UP:13 /= 100
	UP:20 *= 100
	UP:20 /= LOCAL
	UP:21 *= 100
	UP:21 /= LOCAL
	UP:22 *= 100
	UP:22 /= LOCAL
ENDIF

;-------------------------------------------------
;同じコマンドの連続実行による上下の処理
;-------------------------------------------------
;同一コマンド連続実行ペナルティ回避フラグがオンの時は飛ばす
SIF SELECTCOM == PREVCOM && TFLAG:96 == 0
	CALL SEQUENCE_COM_OTHERPALAM

;-------------------------------------------------
;ルーチンワークなコマンド実行を糾弾する嫁
;-------------------------------------------------
;HARD以上の難易度の時に呼び出す
SIF !EQUIP:戦闘中 && FLAG:3 > 2
	CALL CRITICISM_ROUTINE_WORK

;-------------------------------------------------
;残り気力や欲情・抑鬱による上下の処理
;-------------------------------------------------
CALL UP_VIGOR_CHECK

;-------------------------------------------------
;理性チェック
;-------------------------------------------------
CALL CALC_REASON

;-------------------------------------------------
;愛液処理
;-------------------------------------------------
IF !TALENT:死亡
	LOCAL:1 = UP:13 / 35
	LOCAL:1 = TABLE_CALC(LOCAL:1, 200, 2)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 400, 2)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 600, 2)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 800, 3)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 1000, 3)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 1200, 3)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 1400, 4)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 1600, 4)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 1800, 4)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 2000, 5)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 2200, 5)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 2500, 5)
	LOCAL:1 = TABLE_CALC(LOCAL:1, 3000, 10)
	;機械持ちの場合には陥落していなくて快感に素直もないと分泌しずらい(盛り上がりにくい)
	SIF TALENT:機械 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && TALENT:淫乱 == 0 && TALENT:快感に素直 == 0
		TIMES LOCAL:1 , 0.2
	;欲情が10以上上昇ならその20%の液体のソースが加わる（オトコはダメ）
	IF LOCAL:1 > 0 && TALENT:オトコ == 0
		SOURCE:液体追加 += LOCAL:1
		CALL CALC_SOURCE19_2, LOCAL:1
	ENDIF
ENDIF

;-------------------------------------------------
;気力０による理性消耗増加処理([淫魔]は無効)
;-------------------------------------------------
IF BASE:気力 <= 0 && TALENT:淫魔 == 0 && TALENT:理性 && MAXBASE:9 > 0 && !TEQUIP:時間止め
	SIF DOWNBASE:9 < 150
		DOWNBASE:9 = 150
	TIMES DOWNBASE:9 , 2.00
ENDIF

;-------------------------------------------------
;嫉妬しやすいによる上下の処理
;-------------------------------------------------
SIF TALENT:嫉妬しやすい && TALENT:恋慕
	CALL UP_ENVY_CHECK

;-------------------------------------------------
;気力０による体力消耗増加処理([淫魔]は無効)
;-------------------------------------------------
IF BASE:気力 <= 0 && TALENT:淫魔 == 0
	TIMES DOWNBASE:0 , 1.50
	DOWNBASE:0 = MAX( DOWNBASE:0, (FLAG:3 >= 4) ? 100 # 80 )
ENDIF

;-------------------------------------------------
;調教対象の失神チェック及び失神中のUP記録
;-------------------------------------------------
;コンフィグで失神モードがオンで、失神回復していない
IF FLAG:16 & 2 && TFLAG:898 == 0
	;失神中でない
	IF TFLAG:899 == 0
		;失神するかどうかチェック。条件を満たせば失神する
		CALL PASSOUT_CHECK
	;失神したターンの次のターン以降
	ELSEIF TFLAG:899 >= 1
		;TFLAG:890に失神してから目覚めるまでの累積欲情値を記録。失神回復時に使用
		TFLAG:890 += UP:13
		;TFLAG:891に失神してから目覚めるまでの累積苦痛値を記録。失神回復時に使用
		TFLAG:891 += UP:16
		;TFLAG:892に失神してから目覚めるまでの累積薬毒値を記録。失神回復時に使用
		TFLAG:892 += UP:30
	ENDIF
ENDIF

;-------------------------------------------------
;調教対象が勝手にコマンドを実行した場合の処理
;-------------------------------------------------
IF TFLAG:899 == 0
	;TSTR:0に付け加える
	SIF TFLAG:699
		TSTR:0 = 奴隷が我慢できずに勝手に%TSTR:0%
	
	;パラメータの変化
	IF TFLAG:699 == 1
		TIMES UP:11 , 0.75
		TIMES UP:12 , 0.25
		TIMES UP:13 , 0.90
		TIMES UP:14 , 0.05
		TIMES UP:15 , 0.75
		TIMES UP:20 , 0.75
		TIMES UP:22 , 1.10
		TIMES UP:32 , 1.50
	ELSEIF TFLAG:699 == 2
		TIMES UP:11 , 0.50
		TIMES UP:12 , 0.15
		TIMES UP:13 , 1.10
		TIMES UP:14 , 0.05
		TIMES UP:15 , 0.50
		TIMES UP:22 , 0.50
		TIMES UP:32 , 2.50
	ELSEIF TFLAG:699 == 3
		TIMES UP:11 , 0.25
		TIMES UP:12 , 0.05
		TIMES UP:13 , 1.20
		TIMES UP:14 , 0.01
		TIMES UP:15 , 0.25
		TIMES UP:22 , 0.25
		TIMES UP:32 , 4.00
		;犯され実行カウント（この分だけ2回目以降が起こりにくい
		TFLAG:698 += 1
	ENDIF
	
	;フラグのリセット
	TFLAG:699 = 0
ENDIF

;-------------------------------------------------
;欲情からくる・理性の減少
;-------------------------------------------------
CALL CHECK_TRAIN_LUST

;-------------------------------------------------
;苦痛からくる体力・気力・理性の減少
;-------------------------------------------------
CALL CHECK_TRAIN_PAIN

;-------------------------------------------------
;[爆発物]の爆発判定に
;-------------------------------------------------
SIF TALENT:爆発物
	CALL TRAIN_EXPLOSION_CHECK

;-------------------------------------------------
;死亡モード２
;-------------------------------------------------
IF TALENT:死亡
	REPEAT 40
		LOCAL = COUNT
		SIF STRLENS(PALAMNAME:LOCAL) <= 0 || LOCAL == 10
			CONTINUE
		;上昇値
		UP:LOCAL = 0
		;下降値
		DOWN:LOCAL = 0
	REND
ENDIF

;-------------------------------------------------
;素質等による体力・気力減少の補正(奴隷固有処理)
;-------------------------------------------------
CALL CHECK_TRAIN_LIFE_SLAVE

;-------------------------------------------------
;同じコマンドの連続実行による上下の処理(体力系)
;-------------------------------------------------
;同一コマンド連続実行ペナルティ回避フラグがオンの時は飛ばす
SIF SELECTCOM == PREVCOM && TFLAG:96 == 0
	CALL SEQUENCE_COM_LIFE

;-------------------------------------------------
;お香の効果
;-------------------------------------------------
;野外プレイ、お風呂場プレイ、特殊風呂プレイ中は効果無し
SIF TEQUIP:野外プレイ == 0 && TEQUIP:お風呂場プレイ == 0 && TEQUIP:特殊風呂入浴中 == 0 && TEQUIP:89 > 0
	CALL TRAIN_INCENCE

;-------------------------------------------------
;体力・気力の減少の補正(共通処理)及び減少処理
;-------------------------------------------------
CALL CHECK_TRAIN_LIFE, TARGET
SIF (DOWNBASE:MASTER:0 || DOWNBASE:MASTER:1)
	CALL CHECK_TRAIN_LIFE, MASTER
SIF ASSI >= 0 && (DOWNBASE:ASSI:0 || DOWNBASE:ASSI:1)
	CALL CHECK_TRAIN_LIFE, ASSI

;-------------------------------------------------
;膣出し、挿しっぱなしの判定処理
;-------------------------------------------------
CALL TRAIN_CHECK_SHOOT

;-------------------------------------------------
;パラメータ変動による口上
;-------------------------------------------------
CALL DISPLAY_KOJO_MESSAGE_PALAMCNG

;ここからしばらく失神中は通らない処理。ただし失神回復時は通る
IF TFLAG:899 == 0
;-------------------------------------------------
;苦痛快楽経験、奉仕快楽経験のチェック
;-------------------------------------------------
	CALL EXP_GOT_CHECK

;-------------------------------------------------
;連動パラメーターのチェック
;-------------------------------------------------
	CALL VARIABLE_PALAM_CHECK

;-------------------------------------------------
;調教対象の放尿チェック1(恐怖)
;-------------------------------------------------
	SIF UP:17 > 20
		CALL TRAIN_PEE_CHECK_2, (UP:17 / 20)

;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
	IF EQUIP:睡眠薬 == 0
		CALL MARK_GOT_CHECK
		CALL DISPLAY_KOJO_MESSAGE_MARKCNG
	ENDIF

;-------------------------------------------------
;パニックのチェック
;-------------------------------------------------
	;パニック状態下での補正
	IF TFLAG:108 > 0
		CALL PANIC_CALC
	;パニックモードがONの時呼び出される
	ELSEIF FLAG:16 & 1
		CALL PANIC_CHECK
	ENDIF

;-------------------------------------------------
;個別イベントの呼び出し
;-------------------------------------------------
	CALL DISPLAY_KOJO_MESSAGE_COMEVENT
ENDIF

;-------------------------------------------------
;ツボを突く特別イベント
;-------------------------------------------------
SIF SELECTCOM == 236 && (!EQUIP:戦闘中 || !TFLAG:552)
	CALL EQUIP_COM236
	
;-------------------------------------------------
;同じコマンドの連続実行の表示
;-------------------------------------------------
;ペナルティ回避フラグがオンの時は表示を飛ばす
IF SELECTCOM == PREVCOM && TFLAG:96 == 0
	SETCOLOR COLOR("P-PURPLE")
	PRINTL ＜同一コマンド連続実行＞
	RESETCOLOR
ENDIF

;-------------------------------------------------
;相性の表示
;-------------------------------------------------
LOCAL = TRAIN_RELATION_CALC(PLAYER)
SIF LOCAL > 0 && LOCAL != 100
	PRINTFORML ＜相性{LOCAL/100}.\@(LOCAL%100 < 10) ? 0 # \@{LOCAL%100}倍＞

;-------------------------------------------------
;コマンド終了時の調教の成果表示処理
;-------------------------------------------------
;各経験値の入手、パラメータの変動もここで行う
CALL SHOW_TRAIN_RESULT_PLAYER, MASTER
SIF ASSI >= 0
	CALL SHOW_TRAIN_RESULT_PLAYER, ASSI
CALL SHOW_TRAIN_RESULT_SLAVE

;-------------------------------------------------
;寸止めカウント処理
;-------------------------------------------------
SIF NOWEX:0
	EX:20 = 0
SIF NOWEX:1
	EX:21 = 0
SIF NOWEX:2
	EX:22 = 0
SIF NOWEX:3
	EX:23 = 0
IF TFLAG:30
	EX:30 = 0
	EX:40 = 0
ENDIF
SIF TFLAG:31
	EX:31 = 0
IF TFLAG:32
	EX:32 = 0
	EX:43 = 0
ENDIF

;-------------------------------------------------
;パラメータ加算表示後のテキスト表示
;-------------------------------------------------
;失神中
IF TFLAG:899 > 0
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 11
;失神回復時
ELSEIF TFLAG:898 == 1
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 12
	;失神関係のフラグリセット
	VARSET TFLAG, 0, 850, 900
ELSE
	;失敗、ファンブル
	;追加地の文表示。焦らしプレイ、時止め中は不自然になるため呼ばない
	SIF (!EQUIP:戦闘中 || !TFLAG:552) && TEQUIP:焦らしプレイ == 0 && TEQUIP:時間止め == 0 && NOT_PRINT_ADDITIONAL_TEXT(SELECTCOM) == 1
		CALL DISPLAY_KOJO_TRAIN_TEXT_COM
ENDIF

;-------------------------------------------------
;奴隷が異名を取得する処理
;-------------------------------------------------
CALL NICK_SLAVE_CHECK

;-------------------------------------------------
;淫乱系素質取得の条件を記録
;-------------------------------------------------
;上から淫核／淫茎、淫壷、淫尻、淫乳、尿道狂、キス魔、ドＳ、ドＭ、全取得、淫魔
SIF EX:0 >= 30
	TFLAG:104 |= 1
SIF EX:1 >= 30
	TFLAG:104 |= 2
SIF EX:2 >= 30
	TFLAG:104 |= 4
SIF EX:3 >= 30
	TFLAG:104 |= 8
SIF EX:12 >= 10 && PALAM:恥情 >= 30000
	TFLAG:104 |= 16
SIF TEQUIP:媚薬 == 0 && PALAM:習得 >= 30000 && PALAM:恭順 >= 100000 && PALAM:欲情 >= 100000
	TFLAG:104 |= 32
SIF TEQUIP:媚薬 == 0 && PALAM:欲情 >= 30000 && PALAM:苦痛 >= 30000
	TFLAG:104 |= 64
SIF TEQUIP:媚薬 == 0 && PALAM:屈服 >= 30000 && PALAM:苦痛 >= 30000
	TFLAG:104 |= 128
SIF TEQUIP:媚薬 == 0 && PALAM:習得 >= 30000 && PALAM:恭順 >= 100000 && PALAM:欲情 >= 100000 && PALAM:苦痛 >= 30000 && ASSI < 0
	TFLAG:104 |= 256
SIF TEQUIP:媚薬 == 0 && PALAM:欲情 >= 250000 && EX:0 >= 50 && (EX:1 >= 50 || TALENT:オトコ) && EX:2 >= 50 && EX:3 >= 50 && ASSI < 0
	TFLAG:104 |= 512
;依存イベント用
SIF PALAM:恭順 >= 10000 && PALAM:欲情 >= 10000 && PALAM:屈服 >= 10000
	TFLAG:104 |= 1024

;-------------------------------------------------
;開発度上昇による素質変動条件を記録
;-------------------------------------------------
;上からＣ開発度、Ｖ開発度、Ａ開発度、Ｂ開発度、強制精飲絶頂開発度
SIF CFLAG:Ｃ開発度 >= 256 && TALENT:Ｃ敏感 == 0
	TFLAG:106 |= 1
SIF CFLAG:Ｖ開発度 >= 256 && TALENT:Ｖ敏感 == 0
	TFLAG:106 |= 2
SIF CFLAG:Ａ開発度 >= 256 && TALENT:Ａ敏感 == 0
	TFLAG:106 |= 4
SIF CFLAG:Ｂ開発度 >= 256 && TALENT:Ｂ敏感 == 0
	TFLAG:106 |= 8
IF CFLAG:精飲開発度 >= 10000 && TALENT:精愛味覚 == 0
	SIF FLAG:10 && (TFLAG:106 & 16) == 0
		PRINTFORMW %CALLNAME:TARGET%はすっかり精液の味を覚えこんでしまったようだ……
	TFLAG:106 |= 16
ENDIF
;開発度関連をフォローするイベント口上呼び出し
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 19

;-------------------------------------------------
;コマンド終了前の処理群
;-------------------------------------------------
;失敗、ファンブル → 何もしない
IF TFLAG:552 > 0
	TSTR:0 = 実行失敗
	SELECTCOM = 9
ENDIF

;触手服の特別処理
IF TEQUIP:触手服
	SELECTCOM = SELECTCOM:2
ENDIF
;今回のコマンドを「前回のコマンド」とする
CALL RECORD_TRAIN_ROUTE
PREVCOM = SELECTCOM

;前回の調教者が助手か主人か
TFLAG:133 = (ASSIPLAY) ? 1 # 0

;今回の調教で処女喪失フラグ消滅
TFLAG:101 = 0

;時間経過処理
CALL ADD_TRAINING_TIME, 2

;コマンド初回実行のフラグをここで管理
CALL FLAG_COM_FIRST, ((SELECTCOM / 10) + 9000), (SELECTCOM % 10)

DRAWLINE

;-------------------------------------------------
;戦闘イベントの処理群
;-------------------------------------------------
;対象のカウンター
IF EQUIP:戦闘中 && 距離:TARGET <= 距離:PLAYER
	WAIT
	;蹂躙されている少女
	IF EQUIP:睡眠薬 || TEQUIP:緊縛関連 || TEQUIP:三角木馬乗馬中 || TEQUIP:時間止め || TFLAG:899 >= 1
		IF TEQUIP:緊縛関連 == 8 && TEQUIP:MASTER:緊縛関連 == 8
			CALL SELECT_BATTLE_ACTION, TARGET, MASTER
		ELSEIF TEQUIP:緊縛関連 == 8 && ASSI > 0 && TEQUIP:ASSI:緊縛関連 == 8
			CALL SELECT_BATTLE_ACTION, TARGET, ASSI
		ELSE
			CALL SELECT_BATTLE_ACTION, TARGET, PLAYER
		ENDIF
	ELSEIF PLAYER == MASTER && ASSI > 0 && RAND:10 == 0
		CALL SELECT_BATTLE_ACTION, TARGET, ASSI
	ELSEIF ASSI > 0 && PLAYER == ASSI && RAND:10 == 0
		CALL SELECT_BATTLE_ACTION, TARGET, MASTER
	ELSE
		CALL SELECT_BATTLE_ACTION, TARGET, PLAYER
	ENDIF
	DRAWLINE
ENDIF

;距離の選択
IF EQUIP:戦闘中 && TFLAG:107 < 100 && IF_ALIVE(TARGET)
	WAIT
	;睡眠・拘束・三角木馬・時止め・失神（対象）
	IF EQUIP:睡眠薬 || TEQUIP:緊縛関連 || TEQUIP:三角木馬乗馬中 || TEQUIP:時間止め || TFLAG:899 >= 1 || TEQUIP:戦闘状態 == 7 || TEQUIP:戦闘状態 == 109
		PRINTFORM %SHOW_CALLNAME(TARGET)%は動くことができない！（プロット値
		FONTBOLD
		PRINTFORM {距離:TARGET}
		FONTREGULAR
		PRINTFORML ）
		PRINTL 
	ELSEIF TEQUIP:戦闘状態 == 8
		距離:TARGET = RAND(1, 7)
		PRINTFORM %SHOW_CALLNAME(TARGET)%はプロット値
		FONTBOLD
		PRINTFORM {距離:TARGET}
		FONTREGULAR
		PRINTFORML を選択しました
		PRINTL 
	ENDIF
	
	;主人と助手の拘束を解く
	IF TEQUIP:緊縛関連 != 8
		SIF TEQUIP:MASTER:緊縛関連 == 8
			TEQUIP:MASTER:緊縛関連 = 0
		SIF ASSI > 0 && TEQUIP:ASSI:緊縛関連 == 8
			TEQUIP:ASSI:緊縛関連 = 0
	ENDIF
	
	;距離の選択（主人）
	IF TEQUIP:MASTER:緊縛関連 == 8
		PRINTFORM %SHOW_CALLNAME(MASTER)%は動くことができない！（プロット値
		FONTBOLD
		PRINTFORM {距離:MASTER}
		FONTREGULAR
		PRINTFORML ）
	ELSE
		PRINTFORML %SHOW_CALLNAME(MASTER)%のプロット値を選択してください
		;PRINTLC [0]静止した時間 《Mundain》
		;PRINTLC [1]幽霊歩き 《Ghost Walk》
		;PRINTLC [2]影走 《Shadow Run》
		;PRINTLC [3]思考速度 《Neuro Speed》
		;PRINTLC [4]音速 《Sonic Speed》
		;PRINTLC [5]弾速 《Bullet Speed》
		;PRINTLC [6]光速 《Light Speed》
		;PRINTLC [7]超光速 《F.T.L.》
		
		;PRINTLC [0]零
		PRINTLC [1]壱
		PRINTLC [2]弐
		PRINTLC [3]参
		PRINTLC [4]肆
		PRINTLC [5]伍
		PRINTLC [6]陸
		;PRINTLC [7]死地
		
		$INPUT_LOOP_DISTANCE
		INPUT
		IF INRANGE(RESULT, 1, 6)
			距離:MASTER = RESULT
			CLEARLINE 1
			PRINTL 
			PRINTFORM %SHOW_CALLNAME(MASTER)%はプロット値
			FONTBOLD
			PRINTFORM {距離:MASTER}
			FONTREGULAR
			PRINTFORML を選択しました
		ELSE
			CLEARLINE 1
			REUSELASTLINE 
			GOTO INPUT_LOOP_DISTANCE
		ENDIF
	ENDIF

	;距離の選択（助手）
	IF ASSI > 0 && TEQUIP:ASSI:緊縛関連 == 8
		PRINTL 
		PRINTFORM %SHOW_CALLNAME(ASSI)%は動くことができない！（プロット値
		FONTBOLD
		PRINTFORM {距離:ASSI}
		FONTREGULAR
		PRINTFORML ）
	ELSEIF ASSI > 0
		PRINTL 
		PRINTFORML %SHOW_CALLNAME(ASSI)%のプロット値を選択してください
		;PRINTLC [1]幽霊歩き 《Ghost Walk》
		;PRINTLC [2]影走 《Shadow Run》
		;PRINTLC [3]思考速度 《Neuro Speed》
		;PRINTLC [4]音速 《Sonic Speed》
		;PRINTLC [5]弾速 《Bullet Speed》
		;PRINTLC [6]光速 《Light Speed》
		
		PRINTLC [1]壱
		PRINTLC [2]弐
		PRINTLC [3]参
		PRINTLC [4]肆
		PRINTLC [5]伍
		PRINTLC [6]陸
		$INPUT_LOOP_DISTANCE_2
		INPUT
		IF INRANGE(RESULT, 1, 6)
			距離:ASSI = RESULT
			CLEARLINE 1
			PRINTL 
			PRINTFORM %SHOW_CALLNAME(ASSI)%はプロット値
			FONTBOLD
			PRINTFORM {距離:ASSI}
			FONTREGULAR
			PRINTFORML を選択しました
		ELSE
			CLEARLINE 1
			REUSELASTLINE 
			GOTO INPUT_LOOP_DISTANCE_2
		ENDIF
	ENDIF

	;距離の選択（他）
	REPEAT CHARANUM
		SIF COUNT == MASTER
			CONTINUE
		SIF ASSI > 0 && COUNT == ASSI
			CONTINUE
		;睡眠・拘束・時止め・失神（対象）
		IF COUNT == TARGET && (EQUIP:睡眠薬 || TEQUIP:緊縛関連 || TEQUIP:三角木馬乗馬中 || TEQUIP:時間止め || TFLAG:899 >= 1 || TEQUIP:戦闘状態 == 7 || TEQUIP:戦闘状態 == 8 || TEQUIP:戦闘状態 == 109)
			;瞬間拘束を解く…？
			SIF TEQUIP:緊縛関連 == 9 && TEQUIP:触手調教
				TEQUIP:緊縛関連 = 1
			CONTINUE
		ENDIF
		距離:COUNT = RAND(1, 7)
		IF COUNT == TARGET
			PRINTL 
			PRINTFORM %SHOW_CALLNAME(TARGET)%はプロット値
			FONTBOLD
			PRINTFORM {距離:TARGET}
			FONTREGULAR
			PRINTFORML を選択しました
		ENDIF
	REND
	
	;宣言（調教者）
	;1：近寄る　2：距離をとる　3：背後に回る　4：見切り　5：防御
	IF TEQUIP:MASTER:戦闘状態
		LOCAL = 距離:TARGET - 距離:MASTER
		LOCAL:1 = 距離:MASTER
		SELECTCASE TEQUIP:MASTER:戦闘状態
		CASE 1
			;近寄る
			IF LOCAL > 1
				距離:MASTER ++
			ELSEIF LOCAL < -1
				距離:MASTER --
			ENDIF
			距離:MASTER = LIMIT(距離:MASTER, 1, 6)
			SIF 距離:MASTER == LOCAL:1
				GOTO SKILL_END
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『近寄る』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(MASTER)%）
			PRINTFORML %SHOW_CALLNAME(MASTER)%のプロット値が{距離:MASTER}に変わりました
		CASE 2
			;距離をとる
			IF LOCAL > 0
				距離:MASTER --
			ELSEIF LOCAL < 0
				距離:MASTER ++
			ENDIF
			距離:MASTER = LIMIT(距離:MASTER, 1, 6)
			SIF 距離:MASTER == LOCAL:1
				GOTO SKILL_END
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『距離をとる』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(MASTER)%）
			PRINTFORML %SHOW_CALLNAME(MASTER)%のプロット値が{距離:MASTER}に変わりました
		CASE 3
			;背後に回る
			IF LOCAL > 0 && LOCAL < 3
				距離:MASTER += 2
			ELSEIF LOCAL < 0 && LOCAL > -3
				距離:MASTER -= 2
			ENDIF
			距離:MASTER = LIMIT(距離:MASTER, 1, 6)
			SIF 距離:MASTER == LOCAL:1
				GOTO SKILL_END
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『背後に回る』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(MASTER)%）
			PRINTFORML %SHOW_CALLNAME(MASTER)%のプロット値が{距離:MASTER}に変わりました
		ENDSELECT
	ENDIF
	$SKILL_END
	TEQUIP:MASTER:戦闘状態 = 0
	
	;宣言（助手）
	;1：近寄る　2：距離をとる　3：背後に回る　4：見切り　5：防御
	SIF ASSI <= 0
		GOTO SKILL_END_2
	IF TEQUIP:ASSI:戦闘状態
		LOCAL = 距離:TARGET - 距離:ASSI
		LOCAL:1 = 距離:ASSI
		SELECTCASE TEQUIP:ASSI:戦闘状態
		CASE 1
			;近寄る
			IF LOCAL > 1
				距離:ASSI ++
			ELSEIF LOCAL < -1
				距離:ASSI --
			ENDIF
			距離:ASSI = LIMIT(距離:ASSI, 1, 6)
			SIF 距離:ASSI == LOCAL:1
				GOTO SKILL_END_2
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『近寄る』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(ASSI)%）
			PRINTFORML %SHOW_CALLNAME(ASSI)%のプロット値が{距離:ASSI}に変わりました
		CASE 2
			;距離をとる
			IF LOCAL > 0
				距離:ASSI --
			ELSEIF LOCAL < 0
				距離:ASSI ++
			ELSE
				IF RAND:2
					距離:ASSI ++
				ELSE
					距離:ASSI --
				ENDIF
			ENDIF
			距離:ASSI = LIMIT(距離:ASSI, 1, 6)
			SIF 距離:ASSI == LOCAL:1
				GOTO SKILL_END_2
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『距離をとる』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(ASSI)%）
			PRINTFORML %SHOW_CALLNAME(ASSI)%のプロット値が{距離:ASSI}に変わりました
		CASE 3
			;背後に回る
			IF LOCAL > 0 && LOCAL < 3
				距離:ASSI += 2
			ELSEIF LOCAL < 0 && LOCAL > -3
				距離:ASSI -= 2
			ENDIF
			距離:ASSI = LIMIT(距離:ASSI, 1, 6)
			SIF 距離:ASSI == LOCAL:1
				GOTO SKILL_END_2
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『背後に回る』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(ASSI)%）
			PRINTFORML %SHOW_CALLNAME(ASSI)%のプロット値が{距離:ASSI}に変わりました
		ENDSELECT
	ENDIF
	$SKILL_END_2
	SIF ASSI > 0
		TEQUIP:ASSI:戦闘状態 = 0
	
	;宣言（対象）
	IF TEQUIP:TARGET:戦闘状態 < 100
		LOCAL = 距離:PLAYER - 距離:TARGET
		LOCAL:1 = 距離:TARGET
		SELECTCASE TEQUIP:TARGET:戦闘状態
		CASE 1
			;近寄る
			IF LOCAL > 1
				距離:TARGET ++
			ELSEIF LOCAL < -1
				距離:TARGET --
			ENDIF
			距離:TARGET = LIMIT(距離:TARGET, 1, 6)
			SIF 距離:TARGET == LOCAL:1
				GOTO SKILL_END_3
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『近寄る』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(TARGET)%）
			PRINTFORML %SHOW_CALLNAME(TARGET)%のプロット値が{距離:TARGET}に変わりました
		CASE 2
			;距離をとる
			IF LOCAL > 0
				距離:TARGET --
			ELSEIF LOCAL < 0
				距離:TARGET ++
			ELSE
				IF RAND:2
					距離:TARGET ++
				ELSE
					距離:TARGET --
				ENDIF
			ENDIF
			距離:TARGET = LIMIT(距離:TARGET, 1, 6)
			SIF 距離:TARGET == LOCAL:1
				GOTO SKILL_END_3
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『距離をとる』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(TARGET)%）
			PRINTFORML %SHOW_CALLNAME(TARGET)%のプロット値が{距離:TARGET}に変わりました
		CASE 3
			;背後に回る
			IF LOCAL > 0 && LOCAL < 3
				距離:TARGET += 2
			ELSEIF LOCAL < 0 && LOCAL > -3
				距離:TARGET -= 2
			ENDIF
			距離:TARGET = LIMIT(距離:TARGET, 1, 6)
			SIF 距離:TARGET == LOCAL:1
				GOTO SKILL_END_3
			PRINTL 
			SETCOLOR 0xFFA500
			PRINTFORM 『背後に回る』
			RESETCOLOR
			PRINTFORM の効果が発動！（
			PRINTFORML %SHOW_CALLNAME(TARGET)%）
			PRINTFORML %SHOW_CALLNAME(TARGET)%のプロット値が{距離:TARGET}に変わりました
		ENDSELECT
	ENDIF
	$SKILL_END_3
	IF TEQUIP:TARGET:戦闘状態 >= 100
		TEQUIP:TARGET:戦闘状態 -= 100
	ELSE
		TEQUIP:TARGET:戦闘状態 = 0
	ENDIF
	
	;宣言（戦場）
	;PRINTL 
	;SETCOLOR 0xFFA500
	;PRINTFORM 『』
	;RESETCOLOR
	;PRINTFORM の影響で
	;PRINTFORML 
	
	;リセット
	TFLAG:551 = 0
	TFLAG:552 = 0
	
	DRAWLINE
ENDIF

;=============================================================================
;各種処理
;=============================================================================
;-------------------------------------------------
;調教の成果表示
;-------------------------------------------------
;………………………………………………
;調教対象用
;………………………………………………
@SHOW_TRAIN_RESULT_SLAVE
PRINTL 
PRINTFORM ◇
PRINTFORML %SHOW_CALLNAME(TARGET)%◇

;体力・気力・理性の減少
CALL SHOW_TRAIN_BASEDOWN, TARGET

;調教度変動量表示
IF FLAG:12 & 4 && TFLAG:550
	PRINTFORM 調教度\@(TFLAG:550 >= 0) ? ＋ # －\@{ABS(TFLAG:550 / 10)}.{ABS(TFLAG:550 % 10)}
	PRINTL ％
ENDIF
;好感度変動量表示
SIF FLAG:12 & 4 && TFLAG:97
	PRINTFORML 好感度\@(TFLAG:97 >= 0) ? ＋ # －\@{ABS(TFLAG:97), 2}

;コマンド終了時の経験取得表示処理
CALL GAIN_TRAINEXP, TARGET

;調教ソースの表示
CALL SHOW_SOURCE

CALL OUTPUT_TXT("TRAIN")

;パラメータの上昇・下降・表示処理
CALL SHOW_PARAMETR

;[Debug]のチェック
SIF TALENT:998
	CALL DEBUG_CHECK

;………………………………………………
;調教者(主人・助手)用
;………………………………………………
@SHOW_TRAIN_RESULT_PLAYER, ARG
PRINTL 
PRINTFORM ◇
PRINTFORML %SHOW_CALLNAME(ARG)%◇
;体力・気力・理性の減少
CALL SHOW_TRAIN_BASEDOWN, ARG

;コマンド終了時の経験取得表示処理
CALL GAIN_TRAINEXP, ARG

;-------------------------------------------------
;体力・気力・理性の減少の表示処理
;-------------------------------------------------
@SHOW_TRAIN_BASEDOWN, ARG
PRINTFORM 体力 \@(DOWNBASE:ARG:0 >= 0) ? － # ＋\@{ABS(DOWNBASE:ARG:0), 3, LEFT}　気力 \@(DOWNBASE:ARG:1 >= 0) ? － # ＋\@{ABS(DOWNBASE:ARG:1), 3, LEFT}
IF ARG == TARGET && TALENT:ARG:理性
	SIF MAXBASE:ARG:9 > 0
		PRINTFORM 　理性 \@(DOWNBASE:ARG:9 >= 0) ? － # ＋\@{ABS(DOWNBASE:ARG:9), 3, LEFT}
ENDIF
IF TALENT:ARG:死亡
	IF BASE:ARG:底力 > 0
		PRINTFORM 　★瀕死★
	ELSE
		PRINTFORM 　★死亡★
	ENDIF
ELSE
	IF BASE:ARG:体力 < 500
		IF ARG == TARGET && EQUIP:戦闘中
			PRINTFORM 　★疲労困憊★
		ELSE
			PRINTFORM 　★瀕死★
		ENDIF
	ELSEIF BASE:ARG:気力 <= 0
		PRINTFORM 　★気力０★
	ELSE
		PRINT 
	ENDIF
ENDIF
IF ARG == TARGET && TALENT:ARG:理性
	IF MAXBASE:ARG:9 > 0
		PRINTFORM 　\@(BASE:ARG:理性 <= 0) ? ※理性０※ # \@
	ELSE
		PRINT 　※理性崩壊※
	ENDIF
ENDIF
PRINTL 

;-------------------------------------------------
;相性処理
;-------------------------------------------------
@TRAIN_RELATION_CALC(ARG)
#FUNCTION
LOCAL = 0
;[拒絶]
IF TALENT:拒絶
	;[相愛][隷属][壊造人格]
	LOCAL = (TALENT:相愛 || TALENT:隷属 || TALENT:壊造人格) ? 65 # 50
;[SpellCard]
ELSEIF TALENT:スペルカード
	IF TALENT:スペルカード == NO:ARG
		LOCAL = 500
	ELSEIF TALENT:スペルカード == TALENT:ARG:スペルカード
		LOCAL = 250
	ELSE
		;[相愛][隷属][壊造人格]
		LOCAL = (TALENT:相愛 || TALENT:隷属 || TALENT:壊造人格) ? 20 # 5
	ENDIF
ELSE
	LOCAL = RELATION:(NO:ARG)
ENDIF
SIF LOCAL == 0
		LOCAL = 100
IF TALENT:旦那あり
	LOCAL = MAX(LOCAL * 80 / 100, 1)
ELSEIF TALENT:彼氏あり
	LOCAL = MAX(LOCAL * 85 / 100, 1)
ELSEIF TALENT:思い人あり
	LOCAL = MAX(LOCAL * 90 / 100, 1)
ENDIF
RETURNF LOCAL

;-------------------------------------------------
;コマンド終了時の経験取得表示処理
;-------------------------------------------------
@GAIN_TRAINEXP, ARG
REPEAT 100
	SIF TCVAR:ARG:COUNT <= 0
		CONTINUE
	IF COUNT == 39 && TFLAG:55 && ARG == ASSI && (TALENT:ARG:サド || TALENT:ARG:ドＳ)
		PRINTFORML 　加虐調教経験＋{TCVAR:ARG:COUNT}
	ELSE
		PRINTFORML 　%EXPNAME:COUNT, 12, LEFT%＋{TCVAR:ARG:COUNT}
	ENDIF

	;Ｖ経験、Ａ経験、異常経験の変動を記録
	IF COUNT == 0 && (CFLAG:ARG:11 & 1) == 0
		CFLAG:ARG:11 |= 1
	ELSEIF COUNT == 1 && (CFLAG:ARG:11 & 2) == 0
		CFLAG:ARG:11 |= 2
	ELSEIF COUNT == 50 && (CFLAG:ARG:11 & 4) == 0
		CFLAG:ARG:11 |= 4
	ENDIF
	EXP:ARG:COUNT += TCVAR:ARG:COUNT
REND

;-------------------------------------------------
;調教ソースの表示
;-------------------------------------------------
@SHOW_SOURCE
REPEAT 33
	SIF SOURCE:COUNT > 0
		PRINTFORM %SOURCENAME:COUNT%({SOURCE:COUNT}) 
REND
SIF (SOURCE:酒酔い + SOURCE:鬼殺し) > 0
	PRINTFORM 酒酔い({SOURCE:酒酔い + SOURCE:鬼殺し}) 
SIF SOURCE:媚薬浸潤 > 0 && TALENT:MASTER:998
	PRINTFORM 媚薬浸潤({SOURCE:媚薬浸潤}) 
PRINTL 

;-------------------------------------------------
;パラメータの上昇・下降・表示処理
;-------------------------------------------------
@SHOW_PARAMETR
LOCAL:2 = 0
REPEAT 40
	LOCAL = COUNT
	SIF STRLENS(PALAMNAME:LOCAL) <= 0
		CONTINUE

	;上昇値が負の数なら0にする
	UP:LOCAL = MAX(UP:LOCAL, 0)
	;下降値が負の数なら0にする
	DOWN:LOCAL = MAX(DOWN:LOCAL, 0)
	;変数を統一
	LOCAL:1 = UP:LOCAL - DOWN:LOCAL

	;上昇後が1億を越える場合はキャップをかける
	IF (PALAM:LOCAL + LOCAL:1) > 99999999
		LOCAL:1 = MAX(99999999 - LOCAL:1, 0)
		;奴隷異名取得チェック
		IF (LOCAL >= 0 && LOCAL <= 3) || LOCAL == 13
			TFLAG:109 |= 2048
		ELSEIF LOCAL >= 20 && LOCAL <= 22
			TFLAG:109 |= 4096
		ENDIF
	ENDIF


	IF UP:LOCAL || DOWN:LOCAL
		PRINTFORM 　%PALAMNAME:LOCAL%({PALAM:LOCAL, 8}
		
		IF UP:LOCAL
			PRINT  ＋ 
			IF UP:LOCAL >= 10000
				SETCOLOR COLOR("P-RED")
			ELSEIF UP:LOCAL >= 5000
				SETCOLOR COLOR("ORANGE")
			ENDIF
			PRINTFORM {ABS(UP:LOCAL), 8}
		ENDIF
		RESETCOLOR
		IF DOWN:LOCAL
			PRINT  － 
			IF DOWN:LOCAL >= 10000
				SETCOLOR COLOR("P-RED")
			ELSEIF DOWN:LOCAL >= 5000
				SETCOLOR COLOR("ORANGE")
			ENDIF
			PRINTFORM {ABS(DOWN:LOCAL), 8}
		ENDIF
		
		RESETCOLOR
		PRINTFORM  ＝ {LIMIT(PALAM:LOCAL + LOCAL:1, 0, 99999999), 8})　
		PALAM:LOCAL = LIMIT(PALAM:LOCAL + LOCAL:1, 0, 99999999)
		CALL PALAM_MESSAGE, LOCAL

		;改行or表示位置あけ
		;LOCAL:2 += 1
		;SIF (LOCAL:2)%2 == 0
			PRINTL 
	ENDIF
REND
;最後のループで改行してなかったら、ここで改行しておく
;SIF (LOCAL:2)%2 != 0
;	PRINTL 

;-------------------------------------------------
;調教対象の《苦痛》からくる体力・気力・理性の減少の個別処理
;-------------------------------------------------
@CHECK_TRAIN_PAIN
LOCAL:0 = UP:16 / 25
;[痛みに弱い]
IF TALENT:痛みに弱い
	TIMES LOCAL:0 , 1.20
;[痛みに強い]
ELSEIF TALENT:痛みに強い
	TIMES LOCAL:0 , 0.80
ENDIF

LOCAL:1 = LOCAL:0
;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSEIF ABL:マゾっ気 == 1
	TIMES LOCAL:0 , 0.95
	TIMES LOCAL:1 , 0.85
ELSEIF ABL:マゾっ気 == 2
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 0.70
ELSEIF ABL:マゾっ気 == 3
	TIMES LOCAL:0 , 0.80
	TIMES LOCAL:1 , 0.55
ELSEIF ABL:マゾっ気 == 4
	TIMES LOCAL:0 , 0.65
	TIMES LOCAL:1 , 0.40
ELSE
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.25
ENDIF

;[倒錯的]
SIF TALENT:両刀
	TIMES LOCAL:1 , 0.75
;[マゾ]・[ドＭ]
IF TALENT:マゾ && TALENT:ドＭ
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.10
ELSEIF TALENT:マゾ || TALENT:ドＭ
	TIMES LOCAL:0 , 0.60
	TIMES LOCAL:1 , 0.20
ENDIF
;[人形]
IF TALENT:人形
	TIMES LOCAL:0 , 0.75
	TIMES LOCAL:1 , 0.75
ENDIF
;[鬼]
SIF TALENT:鬼
	TIMES LOCAL:0 , 0.75
;ベリージュエル装着時
IF CFLAG:42 & 4
	TIMES LOCAL:0 , 0.90
	TIMES LOCAL:1 , 0.90
ENDIF
;[キョンシー]
SIF TALENT:キョンシー
	LOCAL:0 = 0

;触手調教中でない、かつ調教者が[針さばき]を持っており針を使った場合、軽減
IF TALENT:PLAYER:針さばき && TEQUIP:触手調教 == 0 && SELECTCOM == 222 && (!EQUIP:戦闘中 || !TFLAG:552)
	TIMES LOCAL:0 , 0.20
	TIMES LOCAL:1 , 0.50
ENDIF

DOWNBASE:0 += LOCAL:0
DOWNBASE:1 += LOCAL:1
LOCAL:2 = (LOCAL:0 + LOCAL:1) * 3
LOCAL:2 = TABLE_CALC(LOCAL:2, 1000, 2)
LOCAL:2 = TABLE_CALC(LOCAL:2, 2000, 2)
LOCAL:2 = TABLE_CALC(LOCAL:2, 4000, 2)
LOCAL:2 = TABLE_CALC(LOCAL:2, 8000, 2)
DOWNBASE:9 += LOCAL:2
;調教対象の放尿チェック2(苦痛)
SIF UP:16 > 20
	CALL TRAIN_PEE_CHECK_2, (UP:16 / 20)

;-------------------------------------------------
;調教対象の《欲情》からくる理性の減少の個別処理
;-------------------------------------------------
@CHECK_TRAIN_LUST
LOCAL:0 = UP:13 / 10

;快Ｃ
SIF !UP:0
	TIMES LOCAL:0 , 1.10
;快Ｖ
SIF !UP:1
	TIMES LOCAL:0 , 1.10
;快Ａ
SIF !UP:2
	TIMES LOCAL:0 , 1.10
;快Ｂ
SIF !UP:3
	TIMES LOCAL:0 , 1.10
;快感
SIF !UP:0 && !UP:1 && !UP:2 && !UP:3
	TIMES LOCAL:0 , 2.00

;快感に素直
IF TALENT:快感に素直
	TIMES LOCAL:0 , 0.50
;快感の否定
ELSEIF TALENT:快感の否定
	TIMES LOCAL:0 , 1.50
ENDIF
;ABL:欲望をみる
IF ABL:欲望 == 0
	TIMES LOCAL:0 , 2.00
ELSEIF ABL:欲望 == 1
	TIMES LOCAL:0 , 1.85
ELSEIF ABL:欲望 == 2
	TIMES LOCAL:0 , 1.65
ELSEIF ABL:欲望 == 3
	TIMES LOCAL:0 , 1.45
ELSEIF ABL:欲望 == 4
	TIMES LOCAL:0 , 1.25
ELSE
	TIMES LOCAL:0 , 1.00
ENDIF

;媚薬
IF TEQUIP:媚薬
	;媚薬中毒で効果微強化
	IF TALENT:媚薬中毒
		TIMES LOCAL:0 , 1.50
	ELSE
		TIMES LOCAL:0 , 1.25
	ENDIF
ENDIF
;反抗的
SIF TALENT:反抗的
	TIMES LOCAL:0 , 1.50
;気丈
SIF TALENT:気丈
	TIMES LOCAL:0 , 1.25
;無関心
SIF TALENT:無関心
	TIMES LOCAL:0 , 1.50
;感情乏しい
SIF TALENT:感情乏しい
	TIMES LOCAL:0 , 1.40
;貞操無頓着
SIF TALENT:貞操無頓着
	TIMES LOCAL:0 , 0.75
;抑圧
SIF TALENT:抑圧
	TIMES LOCAL:0 , 1.50
;解放
SIF TALENT:解放
	TIMES LOCAL:0 , 0.50
;抵抗
SIF TALENT:抵抗
	TIMES LOCAL:0 , 1.50
;悪魔
SIF TALENT:悪魔
	TIMES LOCAL:0 , 0.80
;[半白澤](変身中)
SIF TALENT:半白澤 == 2
	TIMES LOCAL:0 , 0.80
;[仙人]
IF TALENT:仙人
	SIF ABL:従順 < 3 && ABL:欲望 < 3
		TIMES LOCAL:0 , 1.30
ENDIF
;失神中
IF TFLAG:899 >= 1
	;理性にマイナス補正
	TIMES LOCAL:0 , 0.25
ENDIF

LOCAL:0 = TABLE_CALC(LOCAL:0, 100, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 400, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 550, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 700, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 850, 2)
LOCAL:0 = TABLE_CALC(LOCAL:0, 1000, 3)
DOWNBASE:9 += MIN(LOCAL:0, 1500)

;-------------------------------------------------
;素質等による体力・気力・理性減少の補正(奴隷固有処理)
;-------------------------------------------------
@CHECK_TRAIN_LIFE_SLAVE
;調教対象が[覚](心の目を閉ざしているか[精神崩壊]は除く)
IF TALENT:覚 == 1 && TALENT:精神崩壊 == 0
	TIMES DOWNBASE:1 , 1.25
	TIMES DOWNBASE:9 , 0.75
ENDIF
;主人による[使い魔]調教時
IF PLAYER == MASTER && TALENT:使い魔
	TIMES DOWNBASE:0 , 0.95
	TIMES DOWNBASE:1 , 0.95
	TIMES DOWNBASE:9 , 0.95
ENDIF
;ベリージュエル装着時
IF CFLAG:42 & 4
	TIMES DOWNBASE:0 , 0.90
	TIMES DOWNBASE:1 , 0.90
	TIMES DOWNBASE:9 , 0.90
ENDIF
;調教対象が[淫魔]
IF TALENT:淫魔
	IF DOWNBASE:0 > DOWNBASE:1
		TIMES DOWNBASE:0 , 0.75
	ELSE
		TIMES DOWNBASE:1 , 0.75
	ENDIF
ENDIF
;調教対象が勝手にコマンドを実行した場合は体力・気力・理性の消費が軽減される
IF TFLAG:699 > 0
	TIMES DOWNBASE:0 , 0.80
	TIMES DOWNBASE:1 , 0.80
	TIMES DOWNBASE:9 , 0.80
ENDIF
;EASYの場合、体調が万全だと1回の調教でいきなり体力が0にならない(ただしキャラ共通補正は受ける)
SIF FLAG:3 == 1 && BASE:体力 == MAXBASE:0 && BASE:気力 == MAXBASE:1 && DOWNBASE:0 >= MAXBASE:0
	DOWNBASE:0 = MAXBASE:0 - 1

;-------------------------------------------------
;体力・気力の減少の補正(共通処理)及び減少処理
;-------------------------------------------------
@CHECK_TRAIN_LIFE, ARG
;………………………………………………
;体力・気力・理性の減少の補正(共通処理)
;………………………………………………
;[喘息]
SIF TALENT:ARG:喘息
	TIMES DOWNBASE:ARG:0 , 1.20
;[巨躯]
SIF TALENT:ARG:巨躯
	TIMES DOWNBASE:ARG:0 , 0.75
;[霊体]
IF TALENT:ARG:霊体
	TIMES DOWNBASE:ARG:0 , 0.50
	TIMES DOWNBASE:ARG:1 , 1.50
ENDIF
;[求聞持]
SIF TALENT:ARG:求聞持
	TIMES DOWNBASE:ARG:0 , 1.20
;[神霊]
IF TALENT:ARG:神霊
	TIMES DOWNBASE:ARG:0 , 0.90
	TIMES DOWNBASE:ARG:1 , 0.90
	TIMES DOWNBASE:ARG:9 , 0.90
ENDIF
;[キョンシー]
IF TALENT:ARG:キョンシー
	TIMES DOWNBASE:ARG:0 , 0.25
	TIMES DOWNBASE:ARG:1 , 0.80
	TIMES DOWNBASE:ARG:9 , 0.80
ENDIF

;………………………………………………
;体力・気力・理性の減少処理
;………………………………………………
REPEAT 2
	DOWNBASE:ARG:COUNT = MAX(DOWNBASE:ARG:COUNT, 0)
	CALL DOWNBASE_CALCULATION, ARG, COUNT
REND

;底力の減少処理
SIF DOWNBASE:ARG:0 > BASE:ARG:体力
	BASE:ARG:底力 = LIMIT(BASE:ARG:底力 + BASE:ARG:体力 - DOWNBASE:ARG:0, BASE:ARG:体力, MAXBASE:ARG:20)

BASE:ARG:体力 = LIMIT(BASE:ARG:体力 - DOWNBASE:ARG:0, 0, MAXBASE:ARG:0)
BASE:ARG:気力 = LIMIT(BASE:ARG:気力 - DOWNBASE:ARG:1, 0, MAXBASE:ARG:1)
DOWNBASE:ARG:9 = MAX(DOWNBASE:ARG:9, 0)
SIF MAXBASE:ARG:9 > 0
	CALL DOWNBASE_CALCULATION, ARG, 9

IF ARG == TARGET
	DOWNBASE:ARG:9 -= 100
	;何もしない
	SIF SELECTCOM == 9
		DOWNBASE:ARG:9 -= 100
	;死亡中・時止め中・失神中は変動なし
	SIF TALENT:死亡 || TEQUIP:時間止め || TFLAG:899 > 0
		DOWNBASE:ARG:9 = 0
ENDIF
	
IF (BASE:ARG:理性 - DOWNBASE:ARG:9) < 0 && MAXBASE:ARG:9 > 0
	MAXBASE:ARG:9 += (BASE:ARG:理性 - DOWNBASE:ARG:9) / 4
ELSEIF DOWNBASE:ARG:9 < 0 && (BASE:ARG:理性 - DOWNBASE:ARG:9) > MAXBASE:ARG:9 && MAXBASE:ARG:9 < 12000 && MAXBASE:ARG:9 > 0 && TALENT:ARG:理性
	MAXBASE:ARG:9 += (BASE:ARG:理性 - DOWNBASE:ARG:9 - MAXBASE:ARG:9) / 8
	MAXBASE:ARG:9 = MIN(MAXBASE:ARG:9, 12000)
ENDIF

BASE:ARG:理性 = LIMIT(BASE:ARG:理性 - DOWNBASE:ARG:9, 0, MAXBASE:ARG:9)

;-------------------------------------------------
;時間経過
;-------------------------------------------------
;EASYか時間停止発動中か自動レベルアップモードONの時は時間経過なし
;NORMALの時はまだゆっくりと時間は流れていく。
@ADD_TRAINING_TIME, ARG
SIF FLAG:3 != 1 && TEQUIP:時間止め == 0 && TALENT:998 == 0
	TFLAG:107 += (FLAG:3 == 2) ? 1 # ARG

;-------------------------------------------------
;パラメータについてのテキスト
;-------------------------------------------------
@PALAM_MESSAGE, ARG
;快Ｃ、快Ｖ、快Ａ、快Ｂは絶頂で大きく増減するので、あまりメッセージを出す意味はない
;潤滑
IF ARG == 10
	IF PALAM:ARG < PALAMLV:2
		PRINT （あまり濡れていません）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （程よく濡れています）
	ELSE
		PRINT （充分に濡れています）
	ENDIF
;習得
ELSEIF ARG == 11
	IF PALAM:ARG < PALAMLV:2
		PRINT （よくわからないようです）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （悦ばせ方を学びつつあります）
	ELSE
		PRINT （充分にコツをつかんだようです）
	ENDIF
;恭順
ELSEIF ARG == 12
	IF PALAM:ARG < PALAMLV:2
		PRINT （好意をもっていません）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （好意を抱きつつあります）
	ELSE
		PRINT （信頼を寄せています）
	ENDIF
;欲情
ELSEIF ARG == 13
	IF PALAM:ARG < PALAMLV:2
		PRINT （気持ちが乗っていません）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （発情しつつあります）
	ELSE
		PRINT （快楽の虜になっています）
	ENDIF
;屈服
ELSEIF ARG == 14
	IF PALAM:ARG < PALAMLV:2
		PRINT （まだ屈する気はないようです）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （やや弱気になっています）
	ELSE
		PRINT （心まで犯されています）
	ENDIF
;恥情
ELSEIF ARG == 15
	IF PALAM:ARG < PALAMLV:2
		PRINT （恥ずかしさは感じていません）
	ELSEIF PALAM:ARG < PALAMLV:3
		PRINT （恥じらいを感じています）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （強い羞恥心を感じています）
	ELSE
		PRINT （死にたいぐらい恥ずかしがっています）
	ENDIF
;苦痛
ELSEIF ARG == 16
	IF PALAM:ARG < PALAMLV:2
		PRINT （痛みは感じていません）
	ELSEIF PALAM:ARG < PALAMLV:3
		PRINT （少し痛がっています）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （痛くてたまらないようです）
	ELSE
		PRINT （気が遠くなるほどの痛みです）
	ENDIF
;恐怖
ELSEIF ARG == 17
	IF PALAM:ARG < PALAMLV:2
		PRINT （平然としています）
	ELSEIF PALAM:ARG < PALAMLV:3
		PRINT （やや怯えています）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （怖くてたまらないようです）
	ELSE
		PRINT （気が狂いそうなほどの恐怖です）
	ENDIF
;反感
ELSEIF ARG == 20
	IF PALAM:ARG < PALAMLV:2
		PRINT （嫌がられてはいません）
	ELSEIF PALAM:ARG < PALAMLV:3
		PRINT （少し嫌がっています）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （嫌われています）
	ELSEIF PALAM:ARG < PALAMLV:5
		PRINT （強い憎悪を感じます）
	ELSE
		PRINT （殺意すら抱いています）
	ENDIF
;不快
ELSEIF ARG == 21
	IF PALAM:ARG < PALAMLV:2
		PRINT （不快感は感じていません）
	ELSEIF PALAM:ARG < PALAMLV:3
		PRINT （いい気分ではありません）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （気持ち悪いようです）
	ELSE
		PRINT （不快感で気が狂いそうです）
	ENDIF
;抑鬱
ELSEIF ARG == 22
	IF PALAM:ARG < PALAMLV:2
		PRINT （落ち込んではいません）
	ELSEIF PALAM:ARG < PALAMLV:3
		PRINT （やや元気に欠けます）
	ELSEIF PALAM:ARG < PALAMLV:4
		PRINT （落ち込んでいます）
	ELSE
		PRINT （全てに絶望しています）
	ENDIF
ENDIF

;-------------------------------------------------
;調教対象の放尿チェック(苦痛・恐怖)
;-------------------------------------------------
@TRAIN_PEE_CHECK_2, ARG
;臆病
SIF TALENT:臆病
	TIMES ARG , 1.30
;反抗的
SIF TALENT:反抗的
	TIMES ARG , 0.60
;プライド高い
IF TALENT:プライド高い
	TIMES ARG , 0.60
;プライド低い
ELSEIF TALENT:プライド低い
	TIMES ARG , 1.30
ENDIF
;自制心
SIF TALENT:自制心
	ARG /= 3
;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES ARG , 1.30
;抑圧
SIF TALENT:抑圧
	TIMES ARG , 0.40
;抵抗
SIF TALENT:抵抗
	TIMES ARG , 0.40
;恥じらい
IF TALENT:恥じらい
	TIMES ARG , 0.50
;恥薄い
ELSEIF TALENT:恥薄い
	TIMES ARG , 1.50
ENDIF
;快感に素直
IF TALENT:快感に素直
	TIMES ARG , 1.30
;快感の否定
ELSEIF TALENT:快感の否定
	TIMES ARG , 0.60
ENDIF
;幼児or幼稚or幼児退行
SIF TALENT:幼児 || TALENT:幼稚 || TALENT:幼児退行
	TIMES ARG , 1.50

;触手インプラント
SIF CFLAG:41 & 64
	TIMES ARG , 1.50

BASE:尿意 = LIMIT(BASE:尿意 + ARG, 0, EJAC - 1)

;-------------------------------------------------
;調教対象の調教度チェック
;-------------------------------------------------
@TARGET_ACCEPTANCE_CHECK
IF CFLAG:3668 > 0
	IF !SOURCE:反感追加
		;死亡中・時止め中・失神中はダメです
		IF !TALENT:死亡 && !TEQUIP:時間止め && TFLAG:899 <= 0
			;睡眠中
			IF EQUIP:睡眠薬
				SOURCE:中毒充足 += CFLAG:3668 / 10
			ELSE
				SOURCE:中毒充足 += CFLAG:3668 / 5
			ENDIF
		ENDIF
	ELSE
		SOURCE:反感追加 = MAX(SOURCE:反感追加 - CFLAG:3668, 0)
	ENDIF
ELSE
	IF SOURCE:中毒充足
		;死亡中・時止め中・失神中はダメです
		IF !TALENT:死亡 && !TEQUIP:時間止め && TFLAG:899 <= 0
			;睡眠中
			IF EQUIP:睡眠薬
				SOURCE:中毒充足 = MAX(SOURCE:中毒充足 + CFLAG:3668 / 10, 0)
			ELSE
				SOURCE:中毒充足 = MAX(SOURCE:中毒充足 + CFLAG:3668 / 5, 0)
			ENDIF
		ENDIF
	;死亡中・睡眠中・時止め中・失神中はダメです
	ELSEIF !TALENT:死亡 && !EQUIP:睡眠薬 && !TEQUIP:時間止め && TFLAG:899 <= 0
		SOURCE:反感追加 -= CFLAG:3668 / 5
	ENDIF
ENDIF