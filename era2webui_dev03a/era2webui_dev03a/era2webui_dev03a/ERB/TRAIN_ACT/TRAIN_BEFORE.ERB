;=============================================================================
;調教開始時に行われる特殊な処理
;=============================================================================
;-----------------------------------------------
;各種変数リセット処理
;-----------------------------------------------
@EVENTTRAIN
#ONLY
;口上表示の個別設定フラグが未設定の場合、使用する口上選択させる
SIF CFLAG:9 <= 0
	CALL SELECT_TEXT_DISPLAY, TARGET

;外見設定
CALL SELECT_HAIR_DISPLAY, TARGET

;助手が酔い潰れている場合、ここで離脱する処理を追加
IF ASSI >= 0 && BASE:ASSI:酔い > MAXBASE:ASSI:8
	PRINTFORML 酔いのせいか%CALLNAME:ASSI%の顔が真っ青だ
	PRINTFORMW しばらく%CALLNAME:ASSI%を休ませることにした……
	ASSI = -1
ENDIF

;主人の射精ゲージを0に
BASE:MASTER:射精 = 0
;いちおう調教対象と助手も
BASE:射精 = 0
SIF ASSI >= 0
	BASE:ASSI:射精 = 0
;噴乳ゲージもリセット
BASE:母乳 = 0
;尿意ゲージもリセット
BASE:尿意 = 0
;底力ゲージもリセット
BASE:底力 = BASE:体力
SIF ASSI >= 0
	BASE:ASSI:底力 = BASE:ASSI:体力
BASE:MASTER:底力 = BASE:MASTER:体力

;睡眠プレイ開始
SIF EQUIP:睡眠薬
	CALL SLEEP_CHECK

;射精フラグ、処女喪失フラグなどをリセット
VARSET TFLAG, 0

;経験フラグもリセット
VARSET TCVAR:TARGET:Ｖ経験, 0, 0, 100
VARSET TCVAR:MASTER:Ｖ経験, 0, 0, 100
SIF ASSI >= 0
	VARSET TCVAR:ASSI:Ｖ経験, 0, 0, 100
	
;調教者は誰か
PLAYER = MASTER

;死亡モード
SIF TALENT:死亡
	TFLAG:899 = 1

;PREVCOMリセット
VARSET PREVCOM, -1, 0, 10

;残り時間設定
TFLAG:107 = 1
SIF EQUIP:戦闘中 == 3
	TFLAG:107 += 89
SIF EQUIP:戦闘中 == 2
	TFLAG:107 += 79
SIF EQUIP:戦闘中 == 1
	TFLAG:107 += 69
;調教中の好感度上昇量に使用
TFLAG:238 = 0
;調教中の調教度上昇量に使用
TFLAG:245 = 0
;調教中の状態
TFLAG:999 = 1

;そのキャラを調教するのが初めてだった場合“調教した事があるフラグ”を立てる
SIF !EQUIP:戦闘中 && CFLAG:1610 == 0
	CFLAG:1610 = 1

;調教された日数記録(ターンまでは無理)
SIF !EQUIP:戦闘中
	CFLAG:46 = DAY

;最後に調教されたキャラを記録(IDで)
FLAG:422 = CFLAG:3109
;助手がおれば最後に助手をしたキャラを記録(IDで)
SIF ASSI >= 0
	FLAG:423 = CFLAG:ASSI:3109

;前回コマンド表示をリセット
TSTR:0 = 

;ぱんくしょん側の処理呼び出し
CALL EVENTTRAIN_PANCTION

;発情ボーナス
IF ASSI >= 0
	IF TALENT:ASSI:常時発情 && !TALENT:ASSI:死亡
		STAIN:ASSI:ヴァギナ |= 1
	ENDIF
ENDIF
IF TALENT:常時発情 && !TALENT:死亡
	STAIN:ヴァギナ |= 1
	PALAM:潤滑 += MIN(CFLAG:58, 4750) + ABL:欲望 * 50
	PALAM:欲情 += MIN(CFLAG:59, 9000) + ABL:欲望 * 200
ENDIF
	
;生理＆初潮
SIF TALENT:MASTER:生理
	STAIN:MASTER:ヴァギナ |= 128
IF ASSI >= 0
	SIF TALENT:ASSI:生理 || (TALENT:ASSI:幼児 && DAY >= (CFLAG:ASSI:誕生日 + 49))
		STAIN:ASSI:ヴァギナ |= 128
ENDIF
IF TALENT:生理 || (TALENT:幼児 && DAY >= (CFLAG:誕生日 + 49))
	STAIN:ヴァギナ |= 128
	PALAM:潤滑 += 100
ENDIF

IF EQUIP:戦闘中
	;残存PALAM, 33
	REPEAT 33
		SIF 残存PALAM:COUNT <= 0
			CONTINUE
		PALAM:COUNT = 残存PALAM:COUNT
	REND
	;残存TEQUIP, 100
	REPEAT 100
		SIF 残存TEQUIP:COUNT <= 0
			CONTINUE
		TEQUIP:COUNT = 残存TEQUIP:COUNT
	REND
	;残存汚れ, 8
	REPEAT 8
		SIF 残存汚れ:COUNT <= 0
			CONTINUE
		STAIN:COUNT = 残存汚れ:COUNT
	REND
	DRAWLINE
	;距離の選択（主人）
	PRINTFORML %SHOW_CALLNAME(MASTER)%のプロット値を選択してください
	;PRINTLC [1]幽霊歩き 《Ghost Walk》
	;PRINTLC [2]影走 《Shadow Run》
	;PRINTLC [3]思考速度 《Neuro Speed》
	;PRINTLC [4]音速 《Sonic Speed》
	;PRINTLC [5]弾速 《Bullet Speed》
	;PRINTLC [6]光速 《Light Speed》
	
	PRINTLC [1]壱
	PRINTLC [2]弐
	PRINTLC [3]参
	PRINTLC [4]肆
	PRINTLC [5]伍
	PRINTLC [6]陸
	$INPUT_LOOP_DISTANCE
	INPUT
	IF INRANGE(RESULT, 1, 6)
		距離:MASTER = RESULT
		CLEARLINE 1
		PRINTL 
		PRINTFORM %SHOW_CALLNAME(MASTER)%はプロット値
		FONTBOLD
		PRINTFORM {距離:MASTER}
		FONTREGULAR
		PRINTFORML を選択しました
	ELSE
		CLEARLINE 1
		REUSELASTLINE 
		GOTO INPUT_LOOP_DISTANCE
	ENDIF

	;距離の選択（助手）
	IF ASSI > 0
		PRINTL 
		PRINTFORML %SHOW_CALLNAME(ASSI)%のプロット値を選択してください
		;PRINTLC [1]幽霊歩き 《Ghost Walk》
		;PRINTLC [2]影走 《Shadow Run》
		;PRINTLC [3]思考速度 《Neuro Speed》
		;PRINTLC [4]音速 《Sonic Speed》
		;PRINTLC [5]弾速 《Bullet Speed》
		;PRINTLC [6]光速 《Light Speed》
		
		PRINTLC [1]壱
		PRINTLC [2]弐
		PRINTLC [3]参
		PRINTLC [4]肆
		PRINTLC [5]伍
		PRINTLC [6]陸
		$INPUT_LOOP_DISTANCE_2
		INPUT
		IF INRANGE(RESULT, 1, 6)
			距離:ASSI = RESULT
			CLEARLINE 1
			PRINTL 
			PRINTFORM %SHOW_CALLNAME(ASSI)%はプロット値
			FONTBOLD
			PRINTFORM {距離:ASSI}
			FONTREGULAR
			PRINTFORML を選択しました
		ELSE
			CLEARLINE 1
			REUSELASTLINE 
			GOTO INPUT_LOOP_DISTANCE_2
		ENDIF
	ENDIF

	;距離の選択（他）
	REPEAT CHARANUM
		SIF COUNT == MASTER
			CONTINUE
		SIF ASSI > 0 && COUNT == ASSI
			CONTINUE
		距離:COUNT = RAND(1, 7)
		IF COUNT == TARGET
			PRINTL 
			PRINTFORM %SHOW_CALLNAME(TARGET)%はプロット値
			FONTBOLD
			PRINTFORM {距離:TARGET}
			FONTREGULAR
			PRINTFORML を選択しました
		ENDIF
	REND
	
	;宣言（調教者）
	PRINTL 
	SETCOLOR 0xFFA500
	PRINTFORM 『東方調教典』
	RESETCOLOR
	PRINTFORM の効果が発動！（
	PRINTFORML %SHOW_CALLNAME(MASTER)%）
	PRINTFORM %SHOW_CALLNAME(TARGET)%
	IF ASSI > 0
		PRINTFORM と
		PRINTFORM %SHOW_CALLNAME(ASSI)%
	ENDIF
	PRINTFORML の全能力が1/10になった！
	
	;宣言（助手）
	IF ASSI > 0 && (TALENT:ASSI:恋慕 || TALENT:ASSI:服従 || TALENT:ASSI:淫乱)
		PRINTL 
		SETCOLOR 0xFFA500
		PRINTFORM 『感情修正』
		RESETCOLOR
		PRINTFORM の効果が発動！（
		PRINTFORML %SHOW_CALLNAME(ASSI)%）
		PRINTFORM この戦闘での
		PRINTFORML %SHOW_CALLNAME(MASTER)%の判定値に1を加算します！
	ENDIF
	
	;宣言（対象）
	PRINTL 
	SETCOLOR 0xFFA500
	PRINTFORM 『東方調教典』
	RESETCOLOR
	PRINTFORM の影響で
	PRINTFORML %SHOW_CALLNAME(TARGET)%の特殊能力が発動できない！
	
	;戦場の選定
	;1 平地。特になし。
	;2 水中。海や川や、プール、血の池地獄など。この戦場では、回避判定にマイナス２の修正がつく。
	;3 高所。ビルの谷間や樹上、断崖絶壁など。この戦場でファンブルすると接近戦ダメージを１点受ける。
	;4 悪天候。嵐や吹雪、ミサイルの雨など。この戦場では、すべての攻撃忍法の間合が１上昇する。
	;5 雑踏。人混みや教室、渋滞中の車道など。この戦場では、行為判定のとき、ファンブル値が１上昇する。
	;6 極地。宇宙や深海、溶岩、魔界など。ゲームマスターは、ラウンドの終わりに１Ｄ６を振る。
	;戦闘開始時からの経過ラウンド以下の目が出ると、この戦場にいると、接近戦ダメージを１点受ける。
	;この戦場から脱落したものは、１Ｄ６を振り、変調表の結果を適用すること。
	戦場 = RAND:7
	SIF 戦場 == 0
		戦場 = 1
		
	;宣言（戦場）
	;PRINTL 
	;SETCOLOR 0xFFA500
	;PRINTFORM 『』
	;RESETCOLOR
	
	DRAWLINE
	WAIT
ELSE
	;残存PALAMリセット
	REPEAT 33
		残存PALAM:COUNT = 0
	REND
	;残存TEQUIPリセット
	REPEAT 100
		残存TEQUIP:COUNT = 0
	REND
	;残存汚れリセット
	REPEAT 8
		残存汚れ:COUNT = 0
	REND
ENDIF

;ハクタク化判定
SIF TIME != 0
	CALL CHANGE_HAKUTAKU

;人狼発毛判定
SIF TIME != 0
	CALL HATUMOU_OOKAMI

CALL OUTPUT_TXT("BEFORE")

;調教開始口上の呼び出し
IF TALENT:死亡
	PRINTFORMW %CALLNAME:TARGET%がぴくりとも動かなくなった……
ELSE
	CALL MESSAGE_TRAIN_START_MAIN
ENDIF

