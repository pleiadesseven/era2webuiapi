;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;弁当関連の処理
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;毒入り弁当の処理及び、嫉妬チョコレートの処理、弁当か毒入りかを判定する処理をここで管理
;=============================================================================
;弁当判定処理
;=============================================================================
;ここでは通常の弁当か毒入り弁当、嫉妬チョコ判定処理のどれに飛ぶのかを決定
;優先順位は毒入り弁当→嫉妬チョコ判定→通常の弁当イベントとなっている
;なお、主人が[薬毒耐性][機械]持ちのときは毒入り弁当イベントが発生しない
@LUNCH_JUDGE
TFLAG:202 = 0
LOCAL = 1

;前提条件：[恋慕]と[服従]が無いかつ反発刻印がLv2以上または従順Lv5未満
IF TALENT:MASTER:薬毒耐性 == 0 && TALENT:MASTER:機械 == 0 && TALENT:恋慕 == 0 && TALENT:服従 == 0 && (MARK:反発刻印 >= 2 || ABL:従順 < 5)
	;対象が[弱味]を持っていないかつメディか[調合知識]か[禁断の知識]持ちの場合、確率は1/2
	IF (TALENT:調合知識 || TALENT:禁断の知識 || NO:TARGET == 1033) && TALENT:弱味 == 0
		LOCAL = RAND:2
		;反発刻印Lv3だと強制的に毒入り弁当に
		SIF MARK:反発刻印 >= 3
			LOCAL = 0
	;対象が[弱味]を持っていないかつ[反抗的]か[抵抗]持ちの場合、確率は1/3
	ELSEIF (TALENT:反抗的 || TALENT:抵抗) && TALENT:弱味 == 0
		LOCAL = RAND:3
		;反発刻印Lv3だと2/3の確率で毒入り弁当に
		SIF MARK:反発刻印 >= 3 && LOCAL == 2
			LOCAL = 0
	;対象が[弱味]か恐怖刻印のどちらかを持っているかつ
	;[調合知識]、[禁断の知識]、[嫉妬]、[狂気]いずれも持っていない場合、確率は1/6
	ELSEIF TALENT:調合知識 == 0 && TALENT:禁断の知識 == 0 && TALENT:嫉妬 == 0 && TALENT:狂気 == 0 && (TALENT:弱味 || MARK:恐怖刻印)
		LOCAL = RAND:6
	;それ以外の場合、確率は1/4
	ELSE
		LOCAL = RAND:4
	ENDIF
ENDIF
IF LOCAL == 0
	JUMP POISONED_LUNCH
ELSEIF FLAG:14 & 4
	JUMP CHOCOLATE_JUDGE
ELSE
	JUMP LUNCH_SALE
ENDIF
RETURN 0

;=============================================================================
;嫉妬チョコ判定処理
;=============================================================================
;ヤンデレパッチ有効のとき、ランダムで嫉妬チョコに変化
;奴隷が[嫉妬]＋[狂気]持ち、[嫉妬]持ち、嫉妬心が芽生えかけている等の状況によって確率は変動
@CHOCOLATE_JUDGE
LOCAL = 0
IF TALENT:精神崩壊 || TALENT:傀儡 || TALENT:壊造人格
	LOCAL = 0
ELSEIF TALENT:嫉妬 && TALENT:狂気
	LOCAL = RAND:8
ELSEIF TALENT:嫉妬
	LOCAL = RAND:4
ELSEIF CFLAG:嫉妬ポイント >= 20
	LOCAL = RAND:3
ELSE
	LOCAL = 2
	LOCAL -= RAND:4
ENDIF

IF LOCAL > 1
	JUMP LUNCH_CHOCOLATE
ELSE
	JUMP LUNCH_SALE
ENDIF

;=============================================================================
;毒入り弁当処理
;=============================================================================
;奴隷に弁当を作らせて食べるオプション。
;-------------------------------------------------
;LOCAL = 料理の種類
;LOCAL:1 = 毒の種類
;LOCAL:2 = 毒の見抜きにくさ補正
;LOCAL:3 = 習得の珠増加量
;LOCAL:4 = 固有料理の種類、乱数処理用
;LOCAL:5～7 = 内部処理用
;-------------------------------------------------
;TFLAG:243の種別
;0 = [禁断の知識]がある場合にそれを喪失する
;1 = [サド]がある場合にそれを喪失する
;3 = [汚れ無視]がある場合にそれを喪失する
;4 = [狂気]がない場合にそれが付加される
;2,5 = [魅力]があれば喪失・なければ[謎の魅力]があれば喪失・なければ[魅惑]があれば喪失する
;6 = 技巧レベルが1レベル低下する(0未満にはならない)
;他 = 体力減少
@POISONED_LUNCH
VARSET LOCAL, 0
LOCAL = RAND:16
LOCAL:1 = RAND:7

;ABL:技巧をみる
LOCAL:2 = ABL:技巧 + 1

;ABL:料理技能をみる
IF ABL:料理技能 == 0
	TIMES LOCAL:2 , 1.00
ELSEIF ABL:料理技能 == 1
	TIMES LOCAL:2 , 1.10
ELSEIF ABL:料理技能 == 2
	TIMES LOCAL:2 , 1.20
ELSEIF ABL:料理技能 == 3
	TIMES LOCAL:2 , 1.30
ELSEIF ABL:料理技能 == 4
	TIMES LOCAL:2 , 1.40
ELSE
	TIMES LOCAL:2 , 1.50
ENDIF

;EXP:調理経験をみる
IF EXP:調理経験 < EXPLV:1
	TIMES LOCAL:2 , 1.00
ELSEIF EXP:調理経験 < EXPLV:2
	TIMES LOCAL:2 , 1.20
ELSEIF EXP:調理経験 < EXPLV:3
	TIMES LOCAL:2 , 1.40
ELSEIF EXP:調理経験 < EXPLV:4
	TIMES LOCAL:2 , 1.60
ELSEIF EXP:調理経験 < EXPLV:5
	TIMES LOCAL:2 , 1.80
ELSE
	TIMES LOCAL:2 , 2.00
ENDIF

IF LOCAL == 0
	TSTR:5 = お弁当
	LOCAL:2 += 12
ELSEIF LOCAL == 1 || LOCAL == 15
	TSTR:5 = 肉じゃが
	LOCAL:2 += 10
ELSEIF LOCAL == 2 || LOCAL == 14
	TSTR:5 = ケーキ
	LOCAL:2 += 8
ELSEIF LOCAL == 3 || LOCAL == 13
	TSTR:5 = 蕎麦
	LOCAL:2 += 6
ELSEIF LOCAL == 4 || LOCAL == 12
	TSTR:5 = 素麺
	LOCAL:2 += 6
ELSEIF LOCAL == 5 || LOCAL == 11
	IF NO:TARGET == 1005
		TSTR:5 = 新鮮野菜のサラダ
		LOCAL:2 += 1
	ELSE
		TSTR:5 = サラダ
	ENDIF
	LOCAL:2 += 4
ELSEIF LOCAL == 6 || LOCAL == 10
	TSTR:5 = ハンバーグ
	LOCAL:2 += 4
ELSE
	TSTR:5 = おにぎり
	LOCAL:2 += 2
ENDIF
LOCAL:2 += RAND:4

;主人の調合知識
SIF TALENT:MASTER:調合知識
	TIMES LOCAL:2 , 2.00

;屈服刻印
SIF MARK:屈服刻印
	LOCAL:2 /= MARK:屈服刻印
;恐怖刻印
SIF MARK:恐怖刻印
	LOCAL:2 /= MARK:恐怖刻印
;反発刻印
SIF MARK:反発刻印
	LOCAL:2 *= MARK:反発刻印
;奉仕精神
SIF ABL:奉仕精神
	LOCAL:2 -= ABL:奉仕精神
;従順
SIF ABL:従順
	LOCAL:2 -= ABL:従順

LOCAL:2 = MAX(LOCAL:2, 4)

;毒を盛ったフラグON
CFLAG:150 |= 2

CALL OUTPUT_TXT("毒料理")
PRINTL 
PRINTFORMW %CALLNAME:TARGET%に料理を作らせた
PRINTL 
;毒入り弁当作成時の口上・地の文の表示
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 410
PRINTL さて……折角の料理だ。
PRINTW ありがたく頂くとしよう……
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　・
PRINTW 　　　　　！
;調合知識なしまたは一定確率で引っかからない
IF TALENT:MASTER:調合知識 == 0 && RAND:(LOCAL:2) > 2
	;毒入り弁当作成時の口上・地の文の表示
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 411

	;主人に毒性発揮
	CALL EFFECT_POISONED_LUNCH, MASTER, LOCAL:1, LOCAL:2
	;ダメージを与えたフラグON
	CFLAG:150 |= 4
	CFLAG:MASTER:150 |= 16
	CFLAG:ストレス = MAX(CFLAG:ストレス - 10, 0)

	;主人の死亡チェック(EASYでないとき主人が調合知識を持たず、毒を盛った奴隷が[弱味]持ちなおかつ恐怖と反発持ちで一定の確率で主人死亡)
	SIF FLAG:3 != 1 && TALENT:MASTER:調合知識 == 0 && TALENT:弱味 && MARK:恐怖刻印 && MARK:反発刻印 && RAND:20 == 0
		CALL BADEND_8
ELSE
	;毒を見破ったフラグON
	CFLAG:150 |= 8
	CFLAG:MASTER:150 |= 32
	;弱味を握るイベント発生判定用
	LOCAL:6 = 0
	LOCAL:7 = 0
	
	REPEAT CHARANUM
		;MASTERは除外
		SIF COUNT == 0
			CONTINUE
			
		;失踪中などこの場にいないなら除外
		SIF CFLAG:COUNT:4
			CONTINUE
		;if dead skip it
		SIF TALENT:COUNT:死亡
			CONTINUE
			
		;[薬毒耐性]か[機械]持ちも除外
		SIF TALENT:COUNT:薬毒耐性 || TALENT:COUNT:機械
			CONTINUE
			
		;助手可能、従順Lv4以上、屈服刻印Lv3、弱味、恋慕系、服従系素質のどれかを持っている必要がある
		SIF ABL:COUNT:従順 < 4 && MARK:COUNT:屈服刻印 < 3 && TALENT:COUNT:弱味 == 0
			CONTINUE
		;妄信、親愛、相愛は当然恋慕所持が大前提なので下の分はかなり重複してる
;		SIF TALENT:COUNT:恋慕 == 0 && TALENT:COUNT:妄信 == 0 && TALENT:COUNT:親愛 == 0 && TALENT:COUNT:相愛 == 0
		SIF TALENT:COUNT:恋慕 == 0 && TALENT:COUNT:服従 == 0
			CONTINUE
		;ここに原因あり
		;烙印、隷属、使い魔(使い魔は、親愛か妄信所持だが、妄信は服従所持が前提)も当然服従所持が大前提
		;よって上記と統合
;		SIF TALENT:COUNT:服従 == 0 && TALENT:COUNT:烙印 == 0 && TALENT:COUNT:隷属 == 0 && TALENT:COUNT:使い魔 == 0
;			CONTINUE
			
		;上の条件を満たすキャラを奴隷の中からピックアップし、調教対象との相性を見比べる
		;ここで調教対象と相性のいいキャラがピックアップされ、2/3の確率で
		;(鬼畜な主人によって)調教対象の毒入り料理を食べさせるように仕向けられる
		;忘れた時用のため
		;NO:ARG … CHARANUMのキャラのcharaXXX.csvのXXXを返す
		LOCAL:5 = NO:COUNT
		
;		SIF RELATION:(LOCAL:5) < 100
;			CONTINUE
		
;		;一番最初にmatchするキャラが現れる頻度が多くなる
;		IF RELATION:(LOCAL:5) > RELATION:(LOCAL:6) && (LOCAL:7 == 0 || RAND:3 > 0)
;			LOCAL:6 = NO:COUNT
;			LOCAL:7 = COUNT
;			SIF TALENT:MASTER:998
;				PRINTFORML LOCAL:7 = {LOCAL:7}
;		ENDIF

		;基本的に、仲の良い子が通る
		IF RELATION:TARGET:(LOCAL:5) > 100
;			SIF TALENT:MASTER:998
;				PRINTFORML %CALLNAME:(LOCAL:5)% = {LOCAL:5}
			;御呼ばれになる子リスト作成
			CALLF SET_CHARA_G(LOCAL:7, COUNT)
			LOCAL:7++
		ENDIF
	REND

	SIF LOCAL:7 > 0
		LOCAL:7 = GET_CHARA_G(RAND:(LOCAL:7))

	IF LOCAL:7 > 0 && RAND:3 > 0
		;一定の確率でTARGETは止めずに逃げようとする
		IF RAND:100 < 5
			;毒の巻き添えを食らったフラグON
			CFLAG:(LOCAL:7):150 |= 8192
			;調合知識持ちまたは一定確率で引っかからない
			IF TALENT:(LOCAL:7):55 == 0 && RAND:(LOCAL:2) > 2
				;どの影響が起きる可能性があるかフラグを設定。口上分岐用
				TFLAG:243 = LOCAL:1
				;毒入り弁当の巻き添えを食らった時の口上・地の文の表示(振舞った方をTFLAG:99に)
				CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, LOCAL:7, 412, TARGET
				;食べたキャラに毒性発揮
				CALL EFFECT_POISONED_LUNCH, LOCAL:7, LOCAL:1, LOCAL:2
			ELSE
				;毒入り弁当を食べた(毒は出ていない)時の口上・地の文の表示(振舞った方をTFLAG:99に)
				CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, LOCAL:7, 413, TARGET
			ENDIF

			;相性低下の前に0％を一時的に100％に
			SIF RELATION:(LOCAL:7):(NO:MASTER) == 0
				RELATION:(LOCAL:7):(NO:MASTER) = 100
			SIF RELATION:(LOCAL:7):(NO:TARGET) == 0
				RELATION:(LOCAL:7):(NO:TARGET) = 100
			;LOCAL:7と主人との相性低下
			RELATION:(LOCAL:7):(NO:MASTER) = MAX(RELATION:(LOCAL:7):(NO:MASTER) - RAND(1, 31), 1)
			;LOCAL:7と毒の首謀者との相性低下
			RELATION:(LOCAL:7):(NO:TARGET) = MAX(RELATION:(LOCAL:7):(NO:TARGET) - RAND(1, 31), 1)
			;相性操作後にもし100％なら表示を考慮して0％に
			SIF RELATION:(LOCAL:7):(NO:MASTER) == 100
				RELATION:(LOCAL:7):(NO:MASTER) = 0
			SIF RELATION:(LOCAL:7):(NO:TARGET) == 100
				RELATION:(LOCAL:7):(NO:TARGET) = 0

			;否定の珠
			CALL COMMON_UP_JUEL, (LOCAL:7), 100, (LOCAL:2*25), 1
		;相性が普通以上なら従来のとおり
		ELSE
			;毒弁当を振舞われそうになった方の口上(毒弁当作成者をTFLAG:99に)
			CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, LOCAL:7, 414, TARGET
		ENDIF
		LOCAL:2 += 12
		IF TALENT:弱味 == 0 && RAND:3 == 0
			PRINTFORMW %CALLNAME:TARGET%の[弱味]を握ることに成功した
			TALENT:弱味 = 1
			CFLAG:12 |= 1024
			LOCAL:2 *= 2
		ENDIF
		SIF TALENT:臆病
			LOCAL:2 *= 2
		SIF TALENT:感情乏しい
			LOCAL:2 /= 2
		IF TALENT:楽観的
			LOCAL:2 -= 5
		ELSEIF TALENT:悲観的
			LOCAL:2 += 10
		ENDIF
		LOCAL:2 = MAX(LOCAL:2, 0)
		;否定の珠
		CALL COMMON_UP_JUEL, TARGET, 100, (LOCAL:2*30), 1
		CFLAG:ストレス += LOCAL:2 * 5
	ELSE
		;毒入り弁当を見破られた時の口上・地の文の表示
		CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 415
		SIF TALENT:MASTER:サド
			LOCAL:2 += 10
		IF TALENT:弱味 == 0 && RAND:8 == 0
			PRINTFORMW %CALLNAME:TARGET%の[弱味]を握ることに成功した
			TALENT:弱味 = 1
			CFLAG:12 |= 1024
			LOCAL:2 *= 2
		ENDIF
		SIF TALENT:臆病
			LOCAL:2 *= 2
		SIF TALENT:感情乏しい
			LOCAL:2 /= 2
		IF TALENT:楽観的
			LOCAL:2 -= 5
		ELSEIF TALENT:悲観的
			LOCAL:2 += 10
		ENDIF
		LOCAL:2 = MAX(LOCAL:2, 0)
		;否定の珠
		CALL COMMON_UP_JUEL, TARGET, 100, (LOCAL:2*20), 1
		CFLAG:ストレス += LOCAL:2*10
	ENDIF
ENDIF

LOCAL:3 = ABL:料理技能 + (1 + RAND:5)
;調理経験
CALL COMMON_UP_EXP, TARGET, 70, LOCAL:3, 1

CFLAG:ストレス = LIMIT(CFLAG:ストレス, 0, 999999)

IF BASE:MASTER:体力 <= 0
	IF TALENT:呪精 || TALENT:蓬莱人 || MARK:同化刻印 || FLAG:5 & 1 || ((TALENT:サド || TALENT:ドＳ || ABL:サドっ気 > 3) && RAND:2 == 0)
		CALL BADEND_7
	ELSE
		CALL BADEND_6
	ENDIF
ENDIF

;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND
RETURN 1

;-------------------------------------------------
;毒入り弁当の効果
;-------------------------------------------------
;ARG＝毒を受けたキャラ、ARG:1＝毒の効果、ARG:2＝毒の強さ(体力減少)
@EFFECT_POISONED_LUNCH, ARG, ARG:1, ARG:2
;[禁断の知識]消滅
IF ARG:1 == 0 && TALENT:ARG:禁断の知識
	PRINTFORMW %CALLNAME:ARG%は[禁断の知識]を失った
	TALENT:ARG:禁断の知識 = 0
;[サド]消滅
ELSEIF ARG:1 == 1 && TALENT:ARG:サド
	PRINTFORMW %CALLNAME:ARG%は[サド]を失った
	TALENT:ARG:サド = 0
;[早漏][遅漏]消滅
ELSEIF ARG:1 == 2 && (TALENT:ARG:早漏 || TALENT:ARG:遅漏)
	IF TALENT:ARG:早漏
		PRINTFORMW %CALLNAME:ARG%は[早漏]を失った
		TALENT:ARG:早漏 = 0
	ELSEIF TALENT:ARG:遅漏
		PRINTFORMW %CALLNAME:ARG%は[遅漏]を失った
		TALENT:ARG:遅漏 = 0
	ENDIF
;[汚れ無視]消滅
ELSEIF ARG:1 == 3 && TALENT:ARG:汚れ無視
	PRINTFORMW %CALLNAME:ARG%は[汚れ無視]を失った
	TALENT:ARG:汚れ無視 = 0
;[狂気]付加
ELSEIF ARG:1 == 4 && TALENT:ARG:狂気 == 0
	PRINTFORMW %CALLNAME:ARG%は[狂気]を得た
	TALENT:ARG:狂気 = 1
	IF ARG != MASTER
		CFLAG:ARG:12 |= 1
		CFLAG:ARG:17 |= 2
		IF TALENT:ARG:理性
			MAXBASE:ARG:9 = MAX(MAXBASE:ARG:9 / 2, 1)
			SIF BASE:ARG:理性 > MAXBASE:ARG:9
				BASE:ARG:理性 = MAXBASE:ARG:9
		ENDIF
	ENDIF
;魅力減少
ELSEIF ARG:1 == 5
	IF TALENT:ARG:大甲斐性
		PRINTFORMW %CALLNAME:ARG%は[大甲斐性]を失った
		TALENT:ARG:大甲斐性 = 0
	ELSEIF TALENT:ARG:謎の魅力
		PRINTFORMW %CALLNAME:ARG%は[謎の魅力]を失った
		TALENT:ARG:謎の魅力 = 0
	ELSEIF TALENT:ARG:魅力
		PRINTFORMW %CALLNAME:ARG%は[魅力]を失った
		TALENT:ARG:魅力 = 0
	ELSEIF TALENT:ARG:人気
		PRINTFORMW %CALLNAME:ARG%は[人気]を失った
		TALENT:ARG:人気 = 0
	ELSEIF TALENT:ARG:魅惑
		PRINTFORMW %CALLNAME:ARG%は[魅惑]を失った
		TALENT:ARG:魅惑 = 0
	;体力減少(小)
	ELSE
		PRINTFORMW %CALLNAME:ARG%は体力を消耗した
		BASE:ARG:体力 -= 75 * (ARG:2 / 5)
	ENDIF
;技能低下
ELSEIF ARG:1 == 6
	;[つるぺた堕し]消滅
	IF TALENT:ARG:大乳導
		PRINTFORMW %CALLNAME:ARG%は[つるぺた堕し]を失った
		TALENT:ARG:大乳導 = 0
	;[大乳導]消滅
	ELSEIF TALENT:ARG:つるぺた堕し
		PRINTFORMW %CALLNAME:ARG%は[大乳導]を失った
		TALENT:ARG:つるぺた堕し = 0
	;[技巧減少
	ELSEIF ABL:ARG:技巧 > 0
		PRINTFORMW %CALLNAME:ARG%の技巧が下がった
		ABL:ARG:技巧 = MAX(ABL:ARG:技巧 - RAND(1, 3), 0)
	;体力減少(小)
	ELSE
		PRINTFORMW %CALLNAME:ARG%は体力を消耗した
		BASE:ARG:体力 -= 75 * (ARG:2 / 5)
	ENDIF
;体力減少(大)
ELSE
	PRINTFORMW %CALLNAME:ARG%は体力を大きく消耗した
	BASE:ARG:体力 -= 200 * (ARG:2 / 3)
ENDIF
;メディは特殊修整あり
IF NO:TARGET == 1033
	BASE:ARG:体力 /= 2
	BASE:ARG:体力 -= 75 * (RAND:5 + 1)
ELSE
	BASE:ARG:体力 -= 75 * (RAND:5 + 1)
ENDIF

;=============================================================================
;嫉妬チョコ処理
;=============================================================================
;ヤンデレな奴隷が作ったチョコを食べたり売ったりするオプション。
;ちゃっかり弁当処理を使い回し
;これぞOperation ～ Bitter Sweet Chocolate
;-------------------------------------------------
;LOCAL = チョコの種類
;LOCAL:1 = 映姫様特別処理用
;LOCAL:2 = チョコの値段の補正を決定
;LOCAL:3 = チョコの値段
;LOCAL:4 = チョコの値段の基礎
;LOCAL:5 = 習得, 否定の珠増加量, 体力の減少量
;LOCAL:6 = 固有チョコの種類、乱数処理用
;LOCAL:7 = 内部処理用
;-------------------------------------------------
@LUNCH_CHOCOLATE
VARSET LOCAL, 0
LOCAL = RAND:16

;ABL:技巧をみる
IF ABL:技巧 == 0
	LOCAL:2 = 10
ELSEIF ABL:技巧 == 1
	LOCAL:2 = 15
ELSEIF ABL:技巧 == 2
	LOCAL:2 = 20
ELSEIF ABL:技巧 == 3
	LOCAL:2 = 30
ELSEIF ABL:技巧 == 4
	LOCAL:2 = 40
ELSE
	LOCAL:2 = 50
ENDIF

;ABL:料理技能をみる
IF ABL:料理技能 == 0
	TIMES LOCAL:2 , 1.00
ELSEIF ABL:料理技能 == 1
	TIMES LOCAL:2 , 1.10
ELSEIF ABL:料理技能 == 2
	TIMES LOCAL:2 , 1.30
ELSEIF ABL:料理技能 == 3
	TIMES LOCAL:2 , 1.50
ELSEIF ABL:料理技能 == 4
	TIMES LOCAL:2 , 1.90
ELSE
	TIMES LOCAL:2 , 2.40
ENDIF

;EXP:調理経験をみる
IF EXP:調理経験 < EXPLV:1
	TIMES LOCAL:2 , 0.80
ELSEIF EXP:調理経験 < EXPLV:2
	TIMES LOCAL:2 , 1.00
ELSEIF EXP:調理経験 < EXPLV:3
	TIMES LOCAL:2 , 1.20
ELSEIF EXP:調理経験 < EXPLV:4
	TIMES LOCAL:2 , 1.40
ELSEIF EXP:調理経験 < EXPLV:5
	TIMES LOCAL:2 , 1.70
ELSE
	TIMES LOCAL:2 , 2.00
ENDIF

;献身的
SIF TALENT:献身的
	TIMES LOCAL:2 , 1.30

;相愛
IF TALENT:相愛
	TIMES LOCAL:2 , 1.80
;親愛
ELSEIF TALENT:親愛
	TIMES LOCAL:2 , 1.50
;恋慕
ELSEIF TALENT:恋慕
	TIMES LOCAL:2 , 1.20
ENDIF

;LOCAL = 料理の種類
IF LOCAL == 0 && ABL:料理技能 >= 5
	;キャラ固有チョコ
	LOCAL:4 = 700
ELSEIF LOCAL == 1 || LOCAL == 2
	;ザッハトルテ
	LOCAL:4 = 630
ELSEIF LOCAL == 3 || LOCAL == 4
	;チョコケーキ
	LOCAL:4 = 540
ELSEIF LOCAL == 5 || LOCAL == 6 || LOCAL == 7
	;ジンジャーチョコレート
	LOCAL:4 = 500
ELSEIF LOCAL == 8 || LOCAL == 9
	;チョコバナナ
	LOCAL:4 = 450
ELSEIF LOCAL == 11 && TALENT:母乳体質
	;母乳チョコ
	LOCAL:4 = 550
ELSE
	;プラリネ
	LOCAL:4 = 400
ENDIF

;調理名人
IF TALENT:調理名人
	TIMES LOCAL:4 , 2.00
;料理得意
ELSEIF TALENT:料理得意
	TIMES LOCAL:4 , 1.50
;料理苦手
ELSEIF TALENT:料理苦手
	TIMES LOCAL:4 , 0.75
ENDIF

;映姫様のみ60日ごとに実行するとボーナスが入る。
IF NO:TARGET == 1036 && (DAY % 60) == 0
	LOCAL = 0
	LOCAL:4 = 600
	LOCAL:1 = 1
ENDIF
IF LOCAL == 0 && (ABL:料理技能 >= 5 || LOCAL:1 == 1)
	;毒を盛ったフラグON
	CFLAG:150 |= 64
	;(専用)口上側で定義されている固有チョコが25%の確率で出る
	IF CFLAG:9 & 8 && STRLENS(CSTR:87) > 0 && RAND:4 == 0
		TSTR:5 = %CSTR:87%
		LOCAL:4 += 88
	;キャラ固有チョコ
	;霊夢様
	ELSEIF NO:TARGET == 1001 && CFLAG:0
		TSTR:5 = 針入りおはぎ
		LOCAL:4 = 777
		LOCAL:5 = 2
	;チルノ
	ELSEIF NO:TARGET == 1005
		TSTR:5 = チョコアイス
		LOCAL:4 += 105
	;美鈴
	ELSEIF NO:TARGET == 1006 || NO:TARGET == 4004
		TSTR:5 = 豆板醤チョコ
		LOCAL:4 += 315
	;こぁ
	ELSEIF NO:TARGET == 1007
		TSTR:5 = ココアチョコ
		LOCAL:4 += 90
	;咲夜さん
	ELSEIF NO:TARGET == 1009
		TSTR:5 = ワインチョコ
		LOCAL:4 += 330
	;フラン
	ELSEIF NO:TARGET == 1011
		;恋の味は苦いって言うよね！…え、言わない？
		TSTR:5 = ビターチョコ
		LOCAL:4 += 80
		SIF TALENT:恋慕
			TFLAG:202 = 1
	;アリス
	ELSEIF NO:TARGET == 1014 && CFLAG:0 == 0
		TSTR:5 = マーブルチョコ
		LOCAL:4 += 165
	;メディスン
	ELSEIF NO:TARGET == 1033
		TSTR:5 = 鈴蘭チョコ
		LOCAL:4 += 80
		LOCAL:5 = 3
	;ヤマメ
	ELSEIF NO:TARGET == 1078
		TSTR:5 = ファンガスティックチョコ
		LOCAL:4 += 99
		LOCAL:5 = 3
	;パルスィ
	ELSEIF NO:TARGET == 1079
		TSTR:5 = ブラッドレッドチョコ
		LOCAL:4 += 88
		LOCAL:5 = 2
	;おくう
	ELSEIF NO:TARGET == 1083
		LOCAL:6 = RAND:6
		IF ITEM:媚薬 > 0 && TALENT:淫乱 && LOCAL:6 == 0
			;媚薬1つ消費
			CALL USE_PLURAL_ITEM, 61, 1
			TFLAG:202 = 1
			TSTR:5 = ヌークリア・チョコ
			LOCAL:4 += 50
			LOCAL:5 = 7
		ELSE
			;うにゅ～
			TSTR:5 = いちごチョコレート
			LOCAL:4 += 60
		ENDIF
	;キャラ
;	ELSEIF NO:TARGET == 
;		TSTR:5 = (料理名)
;		LOCAL:4 += 
	ELSE
		TSTR:5 = 貯古齢糖
	ENDIF
	SIF TALENT:親愛 || TALENT:相愛
		TFLAG:202 = 1
ELSEIF LOCAL == 1 || LOCAL == 2
	TSTR:5 = ザッハトルテ
ELSEIF LOCAL == 3 || LOCAL == 4
	TSTR:5 = チョコケーキ
ELSEIF LOCAL == 5 || LOCAL == 6 || LOCAL == 7
	TSTR:5 = ジンジャーチョコレート
ELSEIF LOCAL == 8 || LOCAL == 9
	TSTR:5 = チョコバナナ
ELSEIF LOCAL == 11 && TALENT:母乳体質
	IF ITEM:媚薬 > 0 && TALENT:淫乱 && RAND:4 == 0
		;媚薬1つ消費
		CALL USE_PLURAL_ITEM, 61, 1
		TFLAG:202 = 1
		LOCAL:4 += 80
		TSTR:5 = ほんのりと甘い香りがする母乳チョコ
	ELSE
		TSTR:5 = 母乳チョコ
	ENDIF
ELSE
	IF ITEM:媚薬 > 0 && TALENT:淫乱 && RAND:4 == 0
		;媚薬1つ消費
		CALL USE_PLURAL_ITEM, 61, 1
		TFLAG:202 = 1
		LOCAL:4 += 80
		TSTR:5 = ほんのりと甘い香りがするプラリネ
	ELSE
		TSTR:5 = プラリネ
	ENDIF
ENDIF

PRINTL 
PRINTFORMW %CALLNAME:TARGET%に料理を作らせた
PRINTL 
;嫉妬チョコ作成時の口上・地の文の表示
CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 420

;-------------------------------------------------
;チョコの値段を計算
;-------------------------------------------------
;普通の弁当よりも補正は大きい
LOCAL:3 = (LOCAL:2 * LOCAL:4) / 25
IF FLAG:3 >= 3
	LOCAL:3 = TABLE_CALC(LOCAL:3, 5000, 2)
	LOCAL:3 = TABLE_CALC(LOCAL:3, 10000, 2)
	LOCAL:3 = TABLE_CALC(LOCAL:3, 50000, 2)
	LOCAL:3 = TABLE_CALC(LOCAL:3, 100000, 2)
ELSE
	LOCAL:3 = TABLE_CALC2(LOCAL:3, 10000, 40)
	LOCAL:3 = TABLE_CALC2(LOCAL:3, 50000, 40)
	LOCAL:3 = TABLE_CALC2(LOCAL:3, 100000, 40)
ENDIF
;恋愛感情(?)補正
SIF TALENT:恋慕
	TIMES LOCAL:3 , 1.20
SIF TALENT:親愛
	TIMES LOCAL:3 , 1.50
SIF TALENT:相愛
	TIMES LOCAL:3 , 1.80
;ヤンデレ補正
SIF TALENT:嫉妬
	TIMES LOCAL:3 , 1.30
SIF TALENT:狂気
	TIMES LOCAL:3 , 1.70

PRINTL 
PRINTL さて……チョコを貰ったのは良かったが…
PRINTFORMW 勇気を出して食べるか、それとも里で{LOCAL:3}圓で売るか……
IF (FLAG:12 & 512) == 0
	PRINTW ……
	PRINTW …………
	PRINTW ………………
ELSE
	PRINTL （どうする？）
	DRAWLINE
	PRINTL [0] - 頑張って食べてみる
	PRINTL [1] - 危険物は処分しよう
	DRAWLINE
	PRINTL 
ENDIF
$INPUT_LOOP
IF (FLAG:12 & 512) == 0
	RESULT = (FLAG:12 & 1024) ? 0 # 1
ELSE
	INPUT
ENDIF
IF RESULT == 0
	;嫉妬チョコを食べてもらえたフラグON
	CFLAG:150 |= 128
	CFLAG:MASTER:150 |= 512
	;作成した嫉妬チョコを主人に食べて貰った時の口上・地の文の表示
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 421
	LOCAL:5 *= ((LOCAL:2 * 5) + (LOCAL:3 / 5))
	LOCAL:5 = MAX(LOCAL:5 - LOCAL:5%100, 100)
	BASE:MASTER:体力 -= LOCAL:5
	PRINTFORML %CALLNAME:MASTER%の体力が{LOCAL:5}減少した。
	IF BASE:MASTER:体力 < 0
		BASE:MASTER:体力 = 1
		PRINTFORML %CALLNAME:MASTER%は死兆星が見えた気がした……
		;異常経験
		CALL COMMON_UP_EXP, MASTER, 50, 1, 1
	ELSEIF BASE:MASTER:体力 < 10
		BASE:MASTER:体力 = 10
	ENDIF
	;ストレス軽減
	IF CFLAG:ストレス
		PRINTFORML %CALLNAME:TARGET%のストレスが低下した。
		CFLAG:ストレス -= LOCAL:5
	ENDIF
ELSEIF RESULT == 1
	;毒を盛ったフラグON
	CFLAG:150 |= 256
	CFLAG:MASTER:150 |= 1024
	;このおにちく！
	PRINTFORML %CALLNAME:TARGET%が\@(TALENT:恋慕) ? 思いを込めて # \@作ったチョコは{LOCAL:3}圓の買値が付いた。
	MONEY:1 = LOCAL:3
	CALL SHOW_MONEY_INCREACE
	;--------------------------------------------------
	;弁当(奴隷)で稼いだ最高金額判定処理
	SIF MONEY:1 > MONEY:996
		CALL NEW_HIGH_INCOME_ALL, 996
	SIF MONEY:1 > CFLAG:最高金額_弁当
		CALL NEW_HIGH_INCOME_SLAVE, 93
	CALL TOTAL_INCOME_SLAVE, MONEY:1
	;--------------------------------------------------

	;作成した嫉妬チョコを売り払われた時の口上・地の文の表示
	CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 422
	CFLAG:嫉妬ポイント += 30
	TFLAG:202 = 0
	LOCAL:5 = (LOCAL:3 / 2 + CFLAG:嫉妬ポイント * 2) * 5

	;妄信
	SIF TALENT:妄信
		TIMES LOCAL:5 , 0.50
	;愛情補正
	IF TALENT:相愛
		TIMES LOCAL:5 , 0.60
	ELSEIF TALENT:親愛
		TIMES LOCAL:5 , 0.75
	ELSEIF TALENT:恋慕
		TIMES LOCAL:5 , 0.90
	ENDIF
	;プライド高い
	SIF TALENT:プライド高い
		TIMES LOCAL:5 , 1.30
	;自制心
	SIF TALENT:自制心
		TIMES LOCAL:5 , 0.90
	;感情乏しい
	SIF TALENT:感情乏しい
		TIMES LOCAL:5 , 0.70
	;悲観的
	IF TALENT:悲観的
		TIMES LOCAL:5 , 1.20
	;楽観的
	ELSEIF TALENT:楽観的
		TIMES LOCAL:5 , 0.80
	ENDIF
	;服従
	SIF TALENT:服従
		TIMES LOCAL:5 , 0.75
	;ヤンデレ補正
	SIF TALENT:嫉妬
		TIMES LOCAL:5 , 1.30
	SIF TALENT:狂気
		TIMES LOCAL:5 , 1.70
	;下2桁は切り捨て
	LOCAL:5 = MAX(LOCAL:5 - (LOCAL:5 % 100), 100)
	IF TALENT:服従 == 0 && TALENT:使い魔 == 0
		IF CFLAG:嫉妬ポイント >= 50 && TALENT:嫉妬 == 0
			TALENT:嫉妬 = 1
			CFLAG:12 |= 4096
			PRINTFORMW ……%CALLNAME:TARGET%の心に嫉妬心が芽生え始めた
		ELSEIF CFLAG:嫉妬ポイント >= 100 && TALENT:狂気 == 0
			TALENT:狂気 = 1
			CFLAG:12 |= 1
			CFLAG:17 |= 2
			PRINTFORMW ……%CALLNAME:TARGET%の嫉妬心は狂気の域に達している
			IF TALENT:理性
				MAXBASE:9 = MAX(MAXBASE:9 / 2, 1)
				SIF BASE:理性 > MAXBASE:9
					BASE:理性 = MAXBASE:9
			ENDIF
		ELSEIF CFLAG:嫉妬ポイント >= 130
			PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい…
			PRINTFORMW 嫌な予感がする…
			TFLAG:202 = 2
		ENDIF
	ENDIF
	IF LOCAL:5 >= 5000 && LOCAL:5 < 12000 && MARK:97 < 1
		LOCAL:6 = 1
	ELSEIF LOCAL:5 >= 12000 && LOCAL:5 < 30000 && MARK:97 < 2
		LOCAL:6 = 2
	ELSEIF LOCAL:5 >= 30000 && MARK:97 < 3
		LOCAL:6 = 3
	ELSE
		LOCAL:6 = 0
	ENDIF
	IF MARK:97 < LOCAL:6
		;反発刻印上昇
		CALL COMMON_MARK_UP, TARGET, 9, LOCAL:6, 0
		LOCAL:7 = 0
		;感情乏しい、傀儡、精神崩壊、壊造人格の場合は反発刻印による従順ダウンはなし
		IF TALENT:感情乏しい == 0 && TALENT:精神崩壊 == 0 && TALENT:傀儡 == 0 && TALENT:壊造人格 == 0
			IF ABL:従順 > 0 && ABL:従順 <= 2 && LOCAL:6 == 3
				LOCAL:7 = 1
				ABL:従順 = 0
			ELSEIF ABL:従順 == 3 && LOCAL:6 == 3
				LOCAL:7 = 1
				ABL:従順 = 2
			ELSEIF ABL:従順 && ABL:従順 <= 2 && LOCAL:6 == 2
				LOCAL:7 = 1
				ABL:従順 -= 1
			ENDIF
		ENDIF
		IF LOCAL:7 == 1
			PRINTFORML そして従順がLV{ABL:従順}になった
			CALL JUJUN_UP_CHECK
		ENDIF
		;嫉妬チョコレートで反発刻印取得
		CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 423, LOCAL:6
	ENDIF
	CFLAG:ストレス += LOCAL:5 * 2 / (2 + RAND:3)
	;否定の珠
	CALL COMMON_UP_JUEL, TARGET, 100, LOCAL:5, 1
ELSE
	GOTO INPUT_LOOP
ENDIF

CFLAG:ストレス = LIMIT(CFLAG:ストレス, 0, 999999)

LOCAL:5 = LOCAL:6 + RAND:5
;調理経験
CALL COMMON_UP_EXP, TARGET, 70, LOCAL:5, 1

;条件を満たすと食後の運動イベントに
IF TFLAG:202 == 1 && ABL:Ｖ感覚 > 0 && FLAG:12 & 1024
	CALL LUNCH_EXERCISE
	;親愛、相愛持ちかつ食後のイベント発生確定時はヤンデレポイントが減少
	;さらに初期に嫉妬を持っていないキャラの場合、嫉妬持ちかつ狂気になっていないなら嫉妬心が消える
	IF TALENT:親愛 || TALENT:相愛
		CFLAG:嫉妬ポイント = MAX(CFLAG:嫉妬ポイント - 20, 0)
		IF CFLAG:嫉妬ポイント == 0 && TALENT:嫉妬 && CFLAG:12 & 4096 && TALENT:COUNT:狂気 == 0
			;嫉妬心氷解時の口上・地の文の表示
			CALL DISPLAY_KOJO_MESSAGE_SUB_EVENT, TARGET, 424
			PRINTFORMW %CALLNAME:TARGET%は[嫉妬]を失った
			TALENT:嫉妬 = 0
			CFLAG:12 -= 4096
		ENDIF
	ENDIF
ELSEIF TFLAG:202 == 2
	SIF TIME == 1
		PRINTW 一日が終わった……
	CALL BADEND_9, TARGET
ELSE
	TFLAG:202 = 0
ENDIF

;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND
RETURN 1
