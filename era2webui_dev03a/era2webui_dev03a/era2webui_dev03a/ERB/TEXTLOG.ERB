;=============================================================================
;調教中のパラメータをテキスト出力
;JSON形式
;時刻,キャラ名,素質,好感度,刻印,行為,絶頂,PALAM増減,PALAM累積値,ほかいろんなフラグ
;GLOBAL:3000 連続呼び出し防止用のタイマー
;GLOBAL:3001 出力テキストにつく連番
;引数
;ARGS どの場面から呼ばれたかを表す文字列 。"scene"要素としてそのまま文字列出力
;ARG 描画キャラの番号 省略したらTARGET
;ローカル変数
;LOCALS  txt出力する文字列
;LOCAL:0～2 カウンタ等に適当に使用
;LOCAL:3 TARGETを一時保管しているので使ったらだめ
;=============================================================================

@OUTPUT_TXT(ARGS,ARG = 0)

;基本的には第1引数だけ指定して呼び出す。
;対象がTARGETでない場面で呼び出すとき第2引数を指定する。（育児部屋など）


;調教メニューお断り
SIF TFLAG:224 == 555
	RETURN 0

;タイマーで200ミリ秒未満の連続実行を防ぐ
GETMILLISECOND
LOCAL = RESULT
SIF GLOBAL:3000 - LOCAL > -200
	RETURN 0


;連続実行防止タイマー更新
GLOBAL:3000 = LOCAL


;対象がTARGETでない場面での呼び出しに対応するため、TAEGETを退避しておく。最後に戻す
LOCAL:3 = TARGET
;引数ありならTARGETを一時的に変更。引数を省略した場合はそのままTARGETを対象にする。
SIF ARG != 0
	TARGET = ARG


;jsonの最初の波括弧を記述------------------------------------------------------
LOCALS = \{

;タイムスタンプ 時刻ミリ秒-----------------------------------------------------
;txtとプロンプトを紐づけるIDにする。今のところ使ってない
LOCALS = %LOCALS%"timestamp":{GLOBAL:3000},

;シーン------------------------------------------------------------------------
;引数の文字列をそのまま渡す
LOCALS = %LOCALS%"scene":"%ARGS%",



;TALENT(取得しているものだけ書き出し)-------------------------------------------
;書式は "talent":["a","b","c",...]

;素質を数えるカウンタ
LOCAL:2 = 0

LOCALS = %LOCALS%"talent":[
;死亡800までループ
REPEAT 801
	LOCAL=COUNT
	SIF STRLENS(TALENTNAME:LOCAL) <= 0
		CONTINUE
	IF TALENT:TARGET:(LOCAL) == 1
		LOCALS = %LOCALS%"%TALENTNAME:(LOCAL)%",
		LOCAL:2 += 1
	ENDIF
	LOCAL += 1
REND
;余分な","を取り除くために末尾1文字を消す処理（素質が1つもなければ実行しない）
SIF LOCAL:2 >= 1
	LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%

LOCALS = %LOCALS%],


;ABL(すべて書き出し)------------------------------------------------------
;書式は "abl":{"a":Lv,"b":Lv,"c":Lv,...}
LOCALS = %LOCALS%"abl":\{
REPEAT 100
	LOCAL=COUNT
	SIF STRLENS(ABLNAME:LOCAL) <= 0
		CONTINUE
	LOCALS = %LOCALS%"%ABLNAME:(LOCAL)%":{ABL:TARGET:LOCAL},
	LOCAL += 1
REND
;1文字消す
LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%
LOCALS = %LOCALS%\},

;刻印-----------------------------------------------------------------------------------
LOCALS = %LOCALS%"mark":\{
REPEAT 11
	LOCAL=COUNT
	SIF STRLENS(MARKNAME:LOCAL) <= 0
		CONTINUE
	LOCALS = %LOCALS%"%MARKNAME:(LOCAL)%":{MARK:TARGET:LOCAL},
	LOCAL += 1
REND
;1文字消す
LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%
LOCALS = %LOCALS%\},

;このコマンドで上昇したPALAM-------------------------------------------------------

LOCALS = %LOCALS%"palam_up":\{
REPEAT 40
	LOCAL = COUNT
	SIF STRLENS(PALAMNAME:LOCAL) <= 0
		CONTINUE
	;上昇値が負の数なら0にする
	UP:LOCAL = MAX(UP:LOCAL, 0)
	;下降値が負の数なら0にする
	DOWN:LOCAL = MAX(DOWN:LOCAL, 0)
	;変数を統一
	LOCAL:1 = UP:LOCAL - DOWN:LOCAL
	LOCALS = %LOCALS%"%PALAMNAME:LOCAL%":{LOCAL:1},
REND
;1文字消す
LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%
LOCALS = %LOCALS%\},

;現在PALAM(呼び出し位置が上昇前なので、UPを足して上昇後の値にする。)----------------------
LOCALS = %LOCALS%"palam":\{
REPEAT 40
	LOCAL = COUNT
	SIF STRLENS(PALAMNAME:LOCAL) <= 0
		CONTINUE
	LOCAL:2 = PALAM:LOCAL + UP:LOCAL
	LOCALS = %LOCALS%"%PALAMNAME:LOCAL%":{LOCAL:2},
REND
;1文字消す
LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%
LOCALS = %LOCALS%\},



;TEQUIP------------------------------------------------------------------
;値が1以上のものすべてについて、変数の番号をそのまま書き出す。
LOCALS = %LOCALS%"equip":\{
LOCAL:2 = 0
REPEAT 100
	LOCAL = COUNT
	SIF TEQUIP:LOCAL < 1
		CONTINUE
	LOCALS = %LOCALS%"{LOCAL}":{TEQUIP:LOCAL},
	LOCAL:2 += 1
REND
SIF LOCAL:2 >= 1
	;1文字消す
	LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%
LOCALS = %LOCALS%\},


;------------------------------------------------------------------------
;コマンドの成功失敗
;YMだとフラグが見当たらないので判定値で計算
;判定のないコマンドではこの値は更新されない

IF TFLAG:240 < TFLAG:241
	LOCALS = %LOCALS%"success":0,
ELSE
	LOCALS = %LOCALS%"success":1,
ENDIF
;PRINTFORML {TFLAG:240} >= {TFLAG:241}


;------------------------------------------------------------------------




;キャラ名称-------------------------------------------------------------
LOCALS = %LOCALS%"target":"%CALLNAME:(TARGET)%",


;ここから単項目で数値型---------------------------------------------------
LOCALS = %LOCALS%"キャラ固有番号":{NO},
LOCALS = %LOCALS%"コマンド":{SELECTCOM},
LOCALS = %LOCALS%"前回コマンド":{PREVCOM},
LOCALS = %LOCALS%"PLAYER":{PLAYER},
LOCALS = %LOCALS%"時間":{TIME},
LOCALS = %LOCALS%"日付":{DAY},
LOCALS = %LOCALS%"体力":{BASE:0},
LOCALS = %LOCALS%"気力":{BASE:1},
LOCALS = %LOCALS%"最大体力":{MAXBASE:0},
LOCALS = %LOCALS%"酔い":{BASE:8},
LOCALS = %LOCALS%"理性":{BASE:9},
LOCALS = %LOCALS%"好感度":{CFLAG:2},
LOCALS = %LOCALS%"出産日":{CFLAG:77},
LOCALS = %LOCALS%"処女喪失":{TFLAG:0},
LOCALS = %LOCALS%"精飲":{TFLAG:3},
LOCALS = %LOCALS%"膣内に射精":{TFLAG:11},
LOCALS = %LOCALS%"アナルに射精":{TFLAG:12},
LOCALS = %LOCALS%"髪に射精":{TFLAG:13},
LOCALS = %LOCALS%"顔に射精":{TFLAG:14},
LOCALS = %LOCALS%"口に射精":{TFLAG:15},
LOCALS = %LOCALS%"胸に射精":{TFLAG:16},
LOCALS = %LOCALS%"腹に射精":{TFLAG:17},
LOCALS = %LOCALS%"腋に射精":{TFLAG:18},
LOCALS = %LOCALS%"手に射精":{TFLAG:19},
LOCALS = %LOCALS%"秘裂に射精":{TFLAG:20},
LOCALS = %LOCALS%"竿に射精":{TFLAG:21},
LOCALS = %LOCALS%"尻に射精":{TFLAG:22},
LOCALS = %LOCALS%"太腿に射精":{TFLAG:23},
LOCALS = %LOCALS%"足で射精":{TFLAG:24},
LOCALS = %LOCALS%"調教対象が噴乳":{TFLAG:31},
LOCALS = %LOCALS%"調教対象が放尿":{TFLAG:32},
LOCALS = %LOCALS%"主人が射精":{TFLAG:35},
LOCALS = %LOCALS%"苦痛刻印取得":{TFLAG:40},
LOCALS = %LOCALS%"快楽刻印取得":{TFLAG:41},
LOCALS = %LOCALS%"屈服刻印取得":{TFLAG:42},
LOCALS = %LOCALS%"恐怖刻印取得":{TFLAG:43},
LOCALS = %LOCALS%"反発刻印取得":{TFLAG:44},
LOCALS = %LOCALS%"薬物刻印取得":{TFLAG:45},
LOCALS = %LOCALS%"恥辱刻印取得":{TFLAG:46},
LOCALS = %LOCALS%"絶頂の強度":{TFLAG:51},
LOCALS = %LOCALS%"今回の調教で処女喪失":{TFLAG:101},
LOCALS = %LOCALS%"膣内射精フラグ":{TFLAG:103},
LOCALS = %LOCALS%"失神":{TFLAG:899},
LOCALS = %LOCALS%"口の汚れ":{STAIN:0},
LOCALS = %LOCALS%"手の汚れ":{STAIN:1},
LOCALS = %LOCALS%"ペニスの汚れ":{STAIN:2},
LOCALS = %LOCALS%"ヴァギナの汚れ":{STAIN:3},
LOCALS = %LOCALS%"アナルの汚れ":{STAIN:4},
LOCALS = %LOCALS%"胸の汚れ":{STAIN:5},
LOCALS = %LOCALS%"足の汚れ":{STAIN:6},
LOCALS = %LOCALS%"髪の汚れ":{STAIN:7},
LOCALS = %LOCALS%"野外プレイの場所":{TFLAG:150},
LOCALS = %LOCALS%"野外プレイの状況":{TFLAG:151},
LOCALS = %LOCALS%"Ｃ絶頂":{NOWEX:0},
LOCALS = %LOCALS%"Ｖ絶頂":{NOWEX:1},
LOCALS = %LOCALS%"Ａ絶頂":{NOWEX:2},
LOCALS = %LOCALS%"Ｂ絶頂":{NOWEX:3},
LOCALS = %LOCALS%"噴乳":{NOWEX:10},
LOCALS = %LOCALS%"放尿":{NOWEX:12},
LOCALS = %LOCALS%"睡眠薬":{K2008F_SLEEPLV("GET")},


;末尾の,を消去してから波括弧を閉じる
LOCALS = %SUBSTRING(LOCALS,0,STRLENS(LOCALS)-1)%
LOCALS = %LOCALS%\}
;PRINTFORML %LOCALS%
;SAVETEXT関数でsavフォルダに出力される。
;SAVETEXT 文字列 連番 
SAVETEXT LOCALS, GLOBAL:3001

;後始末 ARGを省略していたら意味のない処理
TARGET = LOCAL:3

;出力テキストの連番は0～4でループさせる
GLOBAL:3001 = GLOBAL:3001 + 1
SIF GLOBAL:3001 == 5
	GLOBAL:3001 = 0