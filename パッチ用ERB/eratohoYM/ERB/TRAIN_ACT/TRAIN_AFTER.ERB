;=============================================================================
;調教終了時の処理
;=============================================================================
;--------------------------------------------------
;調教の後始末
;--------------------------------------------------
@EVENTEND
#ONLY
CALL OUTPUT_TXT("AFTER")
;調教終了口上の呼び出し
IF TALENT:死亡 || BASE:体力 <= 0
	IF GROUPMATCH(EQUIP:戦闘中, 1, 2, 3)
		DRAWLINE
		EQUIP:戦闘中 = 99
		BASE:体力 = MAX(BASE:体力, 500)
		BASE:底力 = MAX(BASE:底力, 500)
		PRINTFORMW （%CALLNAME:TARGET%の体力が限界に来ています。戦闘に勝利した）
	ENDIF
	DRAWLINE
	PRINTFORMW %CALLNAME:TARGET%がぴくりとも動かなくなった
	PRINTFORMW 何をやっても反応がない……
ELSE
	CALL MESSAGE_TRAIN_END_MAIN
ENDIF

IF GROUPMATCH(EQUIP:戦闘中, 2, 3)
	;残存PALAM, 33
	REPEAT 33
		;快Ｃ、快Ｖ、快Ａ、快Ｂ
		IF GROUPMATCH(COUNT, 0, 1, 2, 3)
			残存PALAM:COUNT = PALAM:COUNT * 6 / 10
		ELSEIF COUNT != 10
			残存PALAM:COUNT = PALAM:COUNT * 8 / 10
		ELSE
			残存PALAM:COUNT = PALAM:COUNT
		ENDIF
	REND
	;残存TEQUIP, 100
	REPEAT 100
		;！デバッグフォース、ＥＸ化、緊縛、眼隠し、猿轡、鼻フック、耳栓、飲水中、三角木馬乗馬中、焦らしプレイ2、焦らしプレイ、時止め、狂気の目、操りの糸
		IF GROUPMATCH(COUNT, 0, 1, 40, 41, 42, 43, 44, 45, 46, 67, 68, 71, 73, 77)
			残存TEQUIP:COUNT = 0
		ELSE
			残存TEQUIP:COUNT = TEQUIP:COUNT
		ENDIF
	REND
	;残存汚れ, 8
	REPEAT 8
		残存汚れ:COUNT = STAIN:COUNT
	REND
ELSE
	;残存PALAMリセット
	REPEAT 33
		残存PALAM:COUNT = 0
	REND
	;残存TEQUIPリセット
	REPEAT 100
		残存TEQUIP:COUNT = 0
	REND
	;残存汚れリセット
	REPEAT 8
		残存汚れ:COUNT = 0
	REND
ENDIF

IF EQUIP:戦闘中
	PRINTL 戦闘を終了しました。
ELSE
	PRINTL 調教を終了しました。
ENDIF

;調教後モードにする
TFLAG:999 = 2

;媚薬風呂の後処理
SIF TEQUIP:特殊風呂入浴中 == 5 && !TALENT:死亡
	CALL POTION_BATH

;産卵プレイの後処理
IF TEQUIP:触手膣内産卵 && TEQUIP:触手腸内産卵 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL DOUBLE_EGGPLAY_OUT
ELSEIF TEQUIP:触手膣内産卵 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL EGGPLAY_OUT
ELSEIF TEQUIP:触手腸内産卵 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL KANTYO_EGGPLAY_OUT
ENDIF

;愛液採取イベント
IF FLAG:12 & 8 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	SIF TEQUIP:ローション == 0 && PALAM:潤滑 >= 30000 && PALAM:欲情 >= 50000 && (CFLAG:10 & 4) == 0 && ITEM:蓋付き瓶 > 0 && TALENT:オトコ == 0
		CALL SWEET_POTATO_LW
ENDIF

;調教後行為のチェック
SIF !TALENT:死亡
	CALL SELF_CHECK_MAIN

;assi lays eggs
SIF ASSI > 0 && (TALENT:ASSI:卵生 || TALENT:ASSI:産卵体質) && TALENT:ASSI:妊娠 == 0 && TALENT:ASSI:懐卵 == 0 && (CFLAG:ASSI:71 || CFLAG:ASSI:72 || CFLAG:ASSI:73 > 0)
	CALL EVENT_LAY_EGG(ASSI)

SIF FLAG:11 & 128 && (TALENT:MASTER:卵生 || TALENT:MASTER:産卵体質) && TALENT:MASTER:妊娠 == 0 && TALENT:MASTER:懐卵 == 0 && (CFLAG:MASTER:71 || CFLAG:MASTER:72 || CFLAG:MASTER:73 > 0)
	CALL EVENT_LAY_EGG(MASTER)

;調教後イベントのチェック
IF !TALENT:死亡
	CALL EVENT_CHECK_AFTER
	PRINTL 
ENDIF

;誰かが巨大化・縮小化していた場合
SIF (TEQUIP:縮小化 || TEQUIP:巨大化) && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL RESIZE

;搾乳した母乳の売却処理
SIF TFLAG:126 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL SELL_MILK
;採尿した聖水の売却処理
SIF TFLAG:127 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL SELL_PEE
;採集で得た愛液の売却処理
SIF TFLAG:129 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL SELL_LOVERWATER
;産卵で得た卵の売却処理
SIF TFLAG:128 && GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CALL SELL_EGG

;調教時に録画したビデオを売却(録画してなければスキップ)
SIF TFLAG:140 & 128 && TFLAG:121 > 0
	CALL SELL_VIDEO

;スイートポテトとワインとアスパラで素敵なディナーを作る秋姉妹
SIF TFLAG:163 > 0
	CALL SELL_DINNER_POTATO
SIF TFLAG:164 > 0
	CALL SELL_DINNER_WINE
SIF TFLAG:165 > 0
	CALL SELL_DINNER_ASPARAGUS

;気力・体力の上昇チェック
SIF FLAG:14 & 2 && BASE:体力 > 0 && !TALENT:死亡 && !EQUIP:戦闘中
	CALL EVENT_GROWTH

;展示品入手イベントの処理--------------------------
;入手した写真を売るかどうか選択
SIF TFLAG:124 > 0
	CALL GET_PICTURE

;入手した陰毛を売るかどうか選択
SIF TFLAG:996 & 64
	CALL GET_PUBLIC_HAIR

;入手したビデオを展示室に
SIF TFLAG:996 & 32
	CALL KEEP_VIDEO

;入手した尻子玉を展示室に
SIF TFLAG:996 & 1
	CALL GET_SHIRIKODAMA

;入手した黄金水を展示室に
SIF TFLAG:996 & 2
	CALL GET_GOLD_WATER

;入手した愛液を展示室に
SIF TFLAG:996 & 4
	CALL GET_LOVE_WATER

;入手した血液を展示室に
SIF TFLAG:996 & 8
	CALL GET_RED_BLOOD

;入手した破瓜血を展示室に
SIF TFLAG:996 & 16
	CALL GET_VIRGIN_BLOOD

;入手した母乳を展示室に
SIF TFLAG:125
	CALL GET_MILK
;--------------------------------------------------
;一定時間調教を行っていれば日常の珠取得判定をリセット
;調教時間の経過しないEASYおよびデバッグモード時は無条件でリセット
SIF (TFLAG:107 > 20 && FLAG:3 > 2) || (TFLAG:107 > 10 && FLAG:3 == 2) || FLAG:3 == 1 || TALENT:998
	FLAG:997 = 0
;[中毒扇動]判定用(一定時間調教を行っており、欲情が10000以上のときオンになる)
SIF ASSI >= 0 && TALENT:ASSI:中毒扇動 && TFLAG:107 > 20 && PALAM:欲情 > 9999
	TFLAG:995 = 1

;常時発情
IF TALENT:常時発情 && !TALENT:死亡
	;潤滑の10000分の1を蓄積　潤滑が10000を下回る場合はリセット
	IF PALAM:潤滑 >= 10000
		CFLAG:58 += PALAM:潤滑 / 10000
	ELSE
		CFLAG:58 = 0
	ENDIF
	;欲情の10000分の1を蓄積　欲情が10000を下回る場合はリセット
	IF PALAM:欲情 >= 10000
		CFLAG:59 += PALAM:欲情 / 10000
	ELSE
		CFLAG:59 = 0
	ENDIF
ENDIF

;マイナスパラメータによるストレス加算
CALL STRESS_CHECK

;何の珠を得られたか
CALL JUEL_CHECK

;《否定の珠》の打ち消しチェック(相殺される珠が無い場合は処理を飛ばす)
SIF JUEL:否定 > 0 && SUMARRAY(JUEL:TARGET:恭順, 12, 18) > 0
	CALL DENIAL_CHECK

;絶頂回数表示
SIF (EX:0+EX:1+EX:2+EX:3+EX:10+EX:11+EX:12+EX:13) > 0
	CALL PRINT_ORGASM

;CONFIGで好感度の表示がＯＮの時のみその調教で変化した好感度を表示する
IF FLAG:12 & 4
	PRINTFORM 今回の調教で%CALLNAME:TARGET%の好感度は
	IF TFLAG:238 == 0
		PRINTL 変化しませんでした。
	ELSE
		PRINTFORML 合計\@(TFLAG:238 >= 0) ? ＋{TFLAG:238} # －{-TFLAG:238}\@変動しました。
	ENDIF
ENDIF

;使用済みコンドームを破棄
SIF GROUPMATCH(EQUIP:戦闘中, 0, 1, 99)
	CFLAG:75 = 0

SIF !TALENT:死亡 || BASE:体力 > 0
	CALL ABL_AFTERTRAIN_MAIN

LOCAL = 0
SIF EQUIP:戦闘中
	LOCAL = 1

EQUIP:戦闘中 = MAX(EQUIP:戦闘中 - 1, 0)

IF EQUIP:戦闘中
	IF EQUIP:戦闘中 <= 3
		PRINTFORML 戦いを続けるか？（あと{EQUIP:戦闘中}回）
		PRINTL [0]はい
		PRINTL [1]いいえ
		$INPUT_LOOP
		INPUT
		IF RESULT == 0
			BEGIN TRAIN
		ELSEIF RESULT == 1
			GOTO TRAIN_END
		ELSE
			CLEARLINE 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
	ELSE
		;勝利した
		EQUIP:戦闘中 = 0
		EQUIP:脱衣 = 0
		BEGIN TURNEND
	ENDIF
ELSE
	$TRAIN_END
	;勝てなかった
	IF LOCAL
		;残存PALAMリセット
		REPEAT 33
			残存PALAM:COUNT = 0
		REND
		;残存TEQUIPリセット
		REPEAT 100
			残存TEQUIP:COUNT = 0
		REND
		;残存汚れリセット
		REPEAT 8
			残存汚れ:COUNT = 0
		REND
		EQUIP:戦闘中 = 0
		EQUIP:脱衣 = 0
		CFLAG:1 = 0
		CFLAG:4 = 8192
		ITEM:(NO:TARGET) = 0
		TARGET = -1
	ENDIF
	BEGIN TURNEND
ENDIF

;--------------------------------------------------
;調教終了時の珠入手処理
;--------------------------------------------------
@JUEL_CHECK
VARSET LOCAL, 0
IF TALENT:死亡
	LOCAL:1 = 0
ELSE
	REPEAT 33
		LOCAL = CALC_JUEL(COUNT)
		SIF LOCAL / 2 >= 1 && (EQUIP:戦闘中 == 2 || EQUIP:戦闘中 == 3)
			LOCAL /= 2
		IF COUNT == 20 || COUNT == 21 || COUNT == 22
			GOTJUEL:100 += LOCAL
		ELSE
			GOTJUEL:COUNT = LOCAL
		ENDIF
		LOCAL:1 += LOCAL
	REND
ENDIF

DRAWLINE
IF LOCAL:1 > 0
	CALL SHOW_GOTJUEL
ELSE
	PRINTW 調教の結果:いずれの珠も得られませんでした。
ENDIF

;--------------------------------------------------
;珠数の計算処理（他で同様の処理があるので関数切り出し）
;--------------------------------------------------
@CALC_JUEL(ARG)
#FUNCTION
VARSET LOCALS,""
SPLIT "0,1,10,100,1000,2000,3000,5000,8000,12000,18000,25000,40000,75000,99999",",",LOCALS
GETPALAMLV PALAM:ARG, (TALENT:恋慕 || TALENT:服従 || TALENT:淫乱) ? 14 # 13
LOCAL = TOINT(LOCALS:RESULT)
SIF RESULT == 1 && PALAM:ARG >= PALAMLV:1*3
	LOCAL += 1
SIF RESULT == 2 && PALAM:ARG >= PALAMLV:2*3
	LOCAL += 10
SIF RESULT == 3 && PALAM:ARG >= PALAMLV:3*2
	LOCAL += 100

IF ARG >= 0 && ARG <= 3
	LOCAL = MIN(LOCAL + EX:ARG * 1000, 99999)
ELSEIF ARG == 20 || ARG == 21 || ARG == 22
	IF LOCAL > 99999 && FLAG:3 > 4
		LOCAL = 99999
	ELSEIF LOCAL > 10000
		LOCAL = 10000
	ENDIF
ENDIF

RETURNF LOCAL

;--------------------------------------------------
;調教終了時の珠表示処理
;--------------------------------------------------
@SHOW_GOTJUEL
#DIM BEFORE_JUEL, 1
PRINTL 調教の結果:
VARSET LOCALS,""
SPLIT "0,1,2,3,11,12,13,14,15,16,17,30,31,32,100",",",LOCALS
LOCAL = 0
REPEAT 15
	LOCAL = TOINT(LOCALS:COUNT)

	;オーバーフロー対策に珠数調整の処理も導入
	;なお、珠の所持は1000万個未満とする(そこまで使うことも無いだろうと見込んで)
	IF JUEL:LOCAL >= 9999999
		GOTJUEL:LOCAL = 0
	ELSEIF (JUEL:LOCAL + GOTJUEL:LOCAL) >= 9999999
		GOTJUEL:LOCAL = 9999999 - JUEL:LOCAL
		;奴隷異名取得チェック
		IF (LOCAL >= 0 && LOCAL <= 3) || LOCAL == 13
			TFLAG:109 |= 2048
		ELSEIF LOCAL == 100
			TFLAG:109 |= 4096
		ENDIF
	ENDIF
	BEFORE_JUEL = JUEL:LOCAL
	GOTJUEL:LOCAL = MAX(GOTJUEL:LOCAL, 0)
	JUEL:LOCAL = LIMIT(JUEL:LOCAL + GOTJUEL:LOCAL, 0, 9999999)

	SIF JUEL:LOCAL + GOTJUEL:LOCAL != 0
		PRINTFORML %PALAMNAME:LOCAL%の珠×({BEFORE_JUEL, 7} \@(GOTJUEL:LOCAL >= 0) ? ＋ # －\@ {ABS(GOTJUEL:LOCAL), 7} -> {JUEL:LOCAL, 7})
REND
PRINTW ……以上の珠を得ました。

;--------------------------------------------------
;否定の珠による打ち消し処理
;--------------------------------------------------
@DENIAL_CHECK
VARSET LOCAL, 0
;入手する珠の変数をリセット
VARSET GOTJUEL:TARGET:0, 0

LOCAL:2 = JUEL:否定

;………………………………………………
;相殺第1段階目
;………………………………………………
SIF JUEL:恭順 + JUEL:欲情 + JUEL:屈服 <= 0
	GOTO LABEL_2
$LABEL_1
LOCAL = RAND(12, 15)
;もし選択された珠がこれ以上相殺できない場合、相殺する珠を再選択する
SIF JUEL:LOCAL == GOTJUEL:LOCAL && SUMARRAY(JUEL:TARGET:恭順, 12, 15) > SUMARRAY(GOTJUEL:TARGET:12, 12, 15)
	GOTO LABEL_1
LOCAL:1 = LIMIT(LOCAL:2 / 2, 1, (JUEL:LOCAL - GOTJUEL:LOCAL))
LOCAL:2 = MAX(LOCAL:2 - LOCAL:1, 0)
;相殺・表示処理用
GOTJUEL:LOCAL += LOCAL:1
GOTJUEL:100 += LOCAL:1
;《否定の珠》が無くなったなら相殺実行・表示処理へ
IF LOCAL:2 == 0
	GOTO LABEL_3
;《恭順の珠》《欲情の珠》《屈服の珠》が残っていると相殺処理を繰り返す
ELSEIF LOCAL:2 > 0 && SUMARRAY(JUEL:TARGET:恭順, 12, 15) > SUMARRAY(GOTJUEL:TARGET:12, 12, 15)
	GOTO LABEL_1
ENDIF

;………………………………………………
;相殺第2段階目
;………………………………………………
SIF JUEL:恥情 + JUEL:苦痛 + JUEL:恐怖 <= 0
	GOTO LABEL_3
$LABEL_2
LOCAL = RAND(15, 18)
;もし選択された珠がこれ以上相殺できない場合、相殺する珠を再選択する
SIF JUEL:LOCAL == GOTJUEL:LOCAL && SUMARRAY(JUEL:TARGET:恥情, 15, 18) > SUMARRAY(GOTJUEL:TARGET:15, 15, 18)
	GOTO LABEL_2
LOCAL:1 = LIMIT(LOCAL:2 / 2, 1, (JUEL:LOCAL - GOTJUEL:LOCAL))
LOCAL:2 = MAX(LOCAL:2 - LOCAL:1, 0)
;相殺・表示処理用
GOTJUEL:LOCAL += LOCAL:1
GOTJUEL:100 += LOCAL:1
;《否定の珠》が無くなったなら相殺実行・表示処理へ
IF LOCAL:2 == 0
	GOTO LABEL_3
;《恥情の珠》《苦痛の珠》《恐怖の珠》が残っていると相殺処理を繰り返す
ELSEIF LOCAL:2 > 0 && SUMARRAY(JUEL:TARGET:恥情, 15, 18) > SUMARRAY(GOTJUEL:TARGET:15, 15, 18)
	GOTO LABEL_2
ENDIF

;………………………………………………
;相殺の実行と内容表示
;………………………………………………
$LABEL_3

SIF !SUMARRAY(GOTJUEL:TARGET:11, 11, 18)
	RETURN 0

DRAWLINE
PRINTL 否定の珠と他の珠の打ち消しが発生しています。その結果、
VARSET LOCALS,""
SPLIT "0,1,2,3,11,12,13,14,15,16,17,30,31,32,100",",",LOCALS
REPEAT 15
	LOCAL = TOINT(LOCALS:COUNT)
	SIF GOTJUEL:LOCAL <= 0
		CONTINUE

	LOCAL:1 = JUEL:LOCAL
	JUEL:LOCAL = LIMIT(JUEL:LOCAL - GOTJUEL:LOCAL, 0, 9999999)
	PRINTFORML %PALAMNAME:LOCAL%の珠×({LOCAL:1, 7} \@(GOTJUEL:LOCAL >= 0) ? － # ＋\@ {ABS(GOTJUEL:LOCAL), 7} -> {JUEL:LOCAL, 7})
REND
PRINTW ……以上のように変化しました。

;--------------------------------------------------
;ストレス加算処理
;--------------------------------------------------
@STRESS_CHECK
SELECTCASE (PALAM:反感 + PALAM:不快 + PALAM:抑鬱) / 2
	CASE IS < 100
		LOCAL:1 = 0
	CASE IS < 200
		LOCAL:1 = 10
	CASE IS < 500
		LOCAL:1 = 25
	CASE IS < 1000
		LOCAL:1 = 50
	CASE IS < 3000
		LOCAL:1 = 75
	CASE IS < 7500
		LOCAL:1 = 100
	CASE IS < 15000
		LOCAL:1 = 250
	CASE IS < 50000
		LOCAL:1 = 500
	CASE IS < 100000
		LOCAL:1 = 800
	CASE IS < 500000
		LOCAL:1 = 1200
	CASE IS < 1000000
		LOCAL:1 = 2400
	CASE IS < 10000000
		LOCAL:1 = 5000
	CASEELSE
		LOCAL:1 = 10000
ENDSELECT

;………………………………………………
;※検討課題
;体力気力が温存されている時こそストレス加算を低めにすべきか？
;なお、現段階では体力気力が摩耗している＝ストレスを感じる余裕すら無いという設定で算出
;………………………………………………
;残り体力の割合
;SELECTCASE (BASE:体力 * 100) / MAXBASE:0
;	CASE IS < 20
;		TIMES LOCAL:1 , 0.80
;	CASE IS < 40
;		TIMES LOCAL:1 , 1.00
;	CASE IS < 60
;		TIMES LOCAL:1 , 1.20
;	CASE IS < 80
;		TIMES LOCAL:1 , 1.40
;	CASEELSE
;		TIMES LOCAL:1 , 1.60
;ENDSELECT
;残り気力の割合
;SELECTCASE (BASE:気力 * 100) / MAXBASE:1
;	CASE IS < 20
;		TIMES LOCAL:1 , 0.75
;	CASE IS < 40
;		TIMES LOCAL:1 , 1.00
;	CASE IS < 60
;		TIMES LOCAL:1 , 1.25
;	CASE IS < 80
;		TIMES LOCAL:1 , 1.50
;	CASEELSE
;		TIMES LOCAL:1 , 1.75
;ENDSELECT

;PALAM:恭順
SELECTCASE PALAM:恭順
	CASE IS < PALAMLV:2
		TIMES LOCAL:1 , 1.00
	CASE IS < PALAMLV:4
		TIMES LOCAL:1 , 0.80
	CASE IS < PALAMLV:6
		TIMES LOCAL:1 , 0.60
	CASEELSE
		TIMES LOCAL:1 , 0.40
ENDSELECT
;PALAM:恐怖
SELECTCASE PALAM:恐怖
	CASE IS < PALAMLV:2
		TIMES LOCAL:1 , 1.00
	CASE IS < PALAMLV:4
		TIMES LOCAL:1 , 1.20
	CASE IS < PALAMLV:6
		TIMES LOCAL:1 , 1.40
	CASEELSE
		TIMES LOCAL:1 , 1.60
ENDSELECT
;屈服刻印
IF MARK:屈服刻印 >= 3
	TIMES LOCAL:1 , 1.75
ELSEIF MARK:屈服刻印 == 2
	TIMES LOCAL:1 , 1.50
ELSEIF MARK:屈服刻印 == 1
	TIMES LOCAL:1 , 1.25
ENDIF

;[臆病]
SIF TALENT:臆病
	TIMES LOCAL:1 , 1.50
;[自制心]
SIF TALENT:自制心
	TIMES LOCAL:1 , 0.75
;[悲観的]
SIF TALENT:悲観的
	TIMES LOCAL:1 , 0.75

;調教度
IF CFLAG:3668 <= -700
	TIMES LOCAL:1 , 1.30
ELSEIF CFLAG:3668 <= -350
	TIMES LOCAL:1 , 1.20
ELSEIF CFLAG:3668 <= 0
	TIMES TFLAG:550, 1.10
ENDIF
	
;理性
IF !TALENT:理性 || EQUIP:睡眠薬 || TFLAG:899 >= 1
	TIMES LOCAL:1 , 0.70
ELSE
	SELECTCASE (BASE:理性 / 120)
		;理性が0以下
		CASE IS <= 0
			TIMES LOCAL:1 , 0.80
		;理性が20未満
		CASE IS < 20
			TIMES LOCAL:1 , 0.90
		;理性が40未満
		CASE IS < 40
			TIMES LOCAL:1 , 1.00
		;理性が60未満
		CASE IS < 60
			TIMES LOCAL:1 , 1.10
		;理性が60未満
		CASE IS < 80
			TIMES LOCAL:1 , 1.20
		;理性が100未満
		CASE IS < 100
			TIMES LOCAL:1 , 1.35
		;理性が100以上
		CASEELSE
			TIMES LOCAL:1 , 1.50
	ENDSELECT
ENDIF

;[死亡]
SIF TALENT:死亡
	LOCAL:1 = 0

CFLAG:ストレス += LOCAL:1
SIF TALENT:MASTER:998 && LOCAL:1
	PRINTFORML ストレス＋{LOCAL:1}
;ストレス値は100万以上にはならない
CFLAG:ストレス = LIMIT(CFLAG:ストレス, 0, 999999)

;--------------------------------------------------
;調教終了時の絶頂表示処理
;--------------------------------------------------
@PRINT_ORGASM
DRAWLINE
PRINTFORML %CALLNAME:TARGET%の調教中の絶頂回数等(調教後行為は含まない)
CALL SHOW_ORGASM
